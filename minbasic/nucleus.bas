  100  REM *** GLOBALS
  110  LET L = 1 : REM LOCATION
  120  LET L1 = 0 : REM NEW LOCATION
  130  DIM T(10) : REM TRAVEL MAP
  140  LET Z(0) = 1 : REM RECURSION COUNTER
  150  DIM Z(100) : REM Z(1,99) PLACES SEEN
  160  DIM Z(200) : REM Z(100,199) OBECT LOCATIONS
  170  DIM Z(209) : REM STATE

  180  REM *** SETUP
  190  FOR I = 1 TO 208
  200    LET Z(I) = 0
  210  NEXT
  220  LET Z(100) = 13 : REM NUMBER OF OBJECTS
  230  LET Z(201) = 1 : REM COMPUTER ON
  240  LET Z(202) = 1 : REM GRATE SAFE
  250  LET Z(203) = 0 : REM DOG TIMER (0 = ASLEEP)
  260  LET Z(204) = 1 : REM FLOODGATE OPEN
  270  LET Z(205) = 0 : REM HOLLOW NOT OPEN
  280  LET Z(206) = 0 : REM CAT NOT FREE
  290  LET Z(207) = 1 : REM WHEEL STUCK
  300  LET Z(208) = 0 : REM DOG ATTACK FLAG

  310  REM OBJECT LOCATIONS
  320  DATA 7, 9, -8, 9, 9, -6, 16, -22, 29, 28, 32, -37, -40
  330  FOR I = 1 TO Z(100)
  340    READ N
  350    LET Z(100 + I) = N
  360  NEXT

  370  REM *** MAIN
  380  PRINT : PRINT 'WELCOME TO ''THE NUCLEUS''!' : PRINT
  390  IF CALL(&YESNO WITH P$ = 'DO YOU WISH INSTRUCTIONS') THEN
  400    GOSUB &DESCR.LONG WITH X = 99
  410    PRINT
  420  ENDIF
  430  GOSUB &DESCR.LONG WITH X = L
  440  WHILE Z(0)
  450    GOSUB &INPUT
  460    LET V = CALL(&FIND.VERB WITH A$)
  470    IF NOT V THEN
  480      IF A$ <> '' GOSUB &PARDON
  490    ELSE
  500      LET L1 = CALL(&FIND.PATH WITH X = V)
  510      IF L1 THEN
  520        GOSUB &TRAVEL
  530      ELSE
  540        GOSUB &VERB
  550      ENDIF
  560      GOSUB &CLOCK
  570    ENDIF
  580  WEND
  590  END

  600  LABEL &CLOCK
  610  IF L = 28 AND NOT Z(203) GOTO &WAKE.DOG
  620  IF Z(208) GOTO &DOG.ATTACK
  630  IF Z(203) GOTO &RUN.DOG
  640  RETURN

  650  LABEL &WAKE.DOG
  660  PRINT 'A MAD DOG APPEARS OUT OF NOWHERE, SNARLS AT YOU AND'
  670  PRINT 'THEN VANISHES IN THE SEMI-DARKNESS OF THE TUNNEL.'
  680  LET Z(203) = 10
  690  RETURN

  700  LABEL &RUN.DOG
  710  LET Z(203) = Z(203) - 1
  720  IF Z(203) = 1 THEN
  730    LET Z(203) = RND(7) + 7
  740    IF L <= 10 OR L >= 36 RETURN
  750    IF L = 22 OR L = 29 OR L = 32 OR L = 34 THEN
  760      PRINT 'YOU HEAR SOME GROWLING NEARBY.' : RETURN
  770    ENDIF
  780    IF Z(206) PRINT 'YOU HEAR SOME DISTANT BARKING.' : RETURN
  790    PRINT 'THE MAD DOG SUDDENLY APPEARS AND ATTACKS YOU!'
  800    LET Z(208) = 1
  810  ENDIF
  820  RETURN

  830  LABEL &DOG.ATTACK
  840  LET Z(208) = 0
  850  IF L <= 10 OR L >= 36 OR L = 22 OR L = 29 OR L = 32 OR L = 34 THEN
  860    PRINT 'YOU ESCAPE THE DOG.' : RETURN
  870  ENDIF
  880  IF RND(10) > 7 THEN
  890    PRINT 'THE DOG WOUNDS YOU SERIOUSLY!'
  900    GOTO &HELL
  910  ELSE
  920    PRINT 'YOU BARELY MANAGE TO FEND THE DOG OFF.'
  930  ENDIF
  940  RETURN

  950  LABEL &TRAVEL : REM L1 = NEW LOCATION
  960  IF L = 3 AND L1 = 8 GOTO &LAB
  970  IF L = 8 AND L1 = 9 GOTO &CLOSET
  980  IF L = 14 AND L1 = 16 GOTO &SUPPLY
  990  IF L1 = 23 GOTO &GRATE
 1000  IF L = 22 AND L1 = 17 AND RND(10) > 5 LET L1 = 30
 1010  IF L = 33 AND L1 = -35 GOTO &AIRVENT
 1020  IF L = 30 AND L1 = 36 AND Z(205) = 0 GOTO &CANTDO
 1030  IF L = 37 AND L1 = 38 AND Z(204) GOTO 3130
 1040  IF L = 39 AND L1 = 38 GOTO &RIVER
 1050  IF L = 43 AND L1 = -50 AND Z(107) <> -43 GOTO 3250
 1060  IF L = 46 AND L1 = -47 GOSUB &TRIVIAL
 1070  IF L1 = 48 GOSUB &ROOF
 1080  LET L = L1
 1090  GOSUB &DESCRIBE WITH X = ABS(L)
 1100  IF L < 0 LET L = ABS(D1) : PRINT : GOTO 1090
 1110  RETURN

 1120  REM *** TRAVEL CONSTRAINTS
 1130  LABEL &LAB
 1140  IF Z(101) = 0 THEN
 1150    PRINT 'YOU OPEN THE DOOR WITH THE KEY CARD.'
 1160    GOTO 1080
 1170  ELSE
 1180    PRINT 'YOU NEED A KEY CARD TO OPEN THE LAB DOOR.'
 1190  ENDIF
 1200  RETURN

 1210  LABEL &CLOSET
 1220  PRINT 'THERE IS A KEYPAD ON THE CLOSET DOOR.'
 1230  PRINT 'ENTER ACCESS CODE: '; : INPUT X
 1240  IF X = 314 THEN
 1250    PRINT 'THE DOOR OPENS.' : PRINT
 1260    GOTO 1080
 1270  ELSE
 1280    PRINT 'BEEEP - WRONG CODE.' : PRINT
 1290  ENDIF
 1300  RETURN

 1310  LABEL &SUPPLY
 1320  IF Z(109) = 0 THEN
 1330    PRINT 'YOU OPEN THE DOOR WITH THE KEY.'
 1340    GOTO 1080
 1350  ELSE
 1360    PRINT 'YOU NEED A KEY TO OPEN THE DOOR.'
 1370  ENDIF
 1380  RETURN

 1390  LABEL &GRATE
 1400  IF Z(202) THEN
 1410    LET Z(202) = 0
 1420    GOTO 1080
 1430  ELSE
 1440    IF RND(100) <= 67 GOTO 1080
 1450    PRINT 'THE RUSTY GRATE SQUEEKS PROTESTINGLY AND THEN'
 1460    PRINT 'COLLAPSES UNDER YOUR WEIGHT. YOU FALL INTO AN'
 1470    PRINT 'AN UNDERGROUND TORRENT AND DROWN.'
 1480    GOTO &HELL
 1490  ENDIF

 1500  LABEL &AIRVENT
 1510  IF Z(111) <> 33 THEN
 1520    PRINT 'YOU CANNOT REACH THE SHAFT.'
 1530    RETURN
 1540  ENDIF
 1550  GOTO 1080

 1560  LABEL &RIVER
 1570  PRINT 'YOU JUMP INTO THE RIVER, WHICH TURNS INTO A SHAFT JUST'
 1580  PRINT 'BEYOND THE TUNNEL ENTRANCE. YOU FALL DOWN THE SHAFT.'
 1590  GOTO 5040

 1600  LABEL &ROOF
 1610  PRINT 'A KEYPAD NEXT TO THE FLOOR BUTTONS FLASHES.'
 1620  PRINT 'ENTER ACCESS CODE: '; : INPUT X
 1630  IF X <> 411 LET L1 = L : GOTO 1280
 1640  LET Z(0) = Z(0) - 1
 1650  IF Z(0) LET L1 = 46
 1660  PRINT : RETURN

 1670  LABEL &TRIVIAL
 1680  LET Z(0) = Z(0) + 1, Z(10) = 0
 1690  IF NOT Z(201) OR Z(0) <= 2 RETURN
 1700  PRINT 'AS YOU TRY TO ENTER THE ELEVATOR, A GLOWING SIGN'
 1710  PRINT 'APPEARS IN FRONT OF YOU. IT SAYS:' : PRINT
 1720  PRINT '   ERR 411:'
 1730  PRINT '   THOU SHALT NOT RECURSE!' : PRINT
 1740  PRINT 'SOMEHOW YOU ARE UNABLE TO ENTER THE ELEVATOR.' : PRINT
 1750  LET L1 = 46, Z(0) = Z(0) - 1
 1760  RETURN

 1770  LABEL &HELL
 1780  PRINT : PRINT 'YOU DIE.' : PRINT
 1790  PRINT '%-( '; : GOSUB &INPUT
 1800  IF A$ = 'QUIT' THEN
 1810    PRINT 'YOU CANNOT EVEN DO *THIS* WHILE YOU ARE DEAD,'
 1820    PRINT 'BUT LET US ASSUME THAT YOU CAN.'
 1830    LET Z(0) = 0, Z(208) = 0 : RETURN
 1840  ELSE
 1850    IF A$ = 'BACK' THEN
 1860      PRINT 'HERE''S YOUR SECOND CHANCE!'
 1870      GOTO 2230
 1880    ELSE
 1890      PRINT 'YOU CAN''T DO THIS WHILE YOU ARE DEAD.'
 1900    ENDIF
 1910  ENDIF
 1920  GOTO 1790

 1930  LABEL &VERB : REM X = VERB ID
 1940  ON X - 999 GOTO &TAKE, &DROP, &LOOK, &QUIT, &INV
 1950  ON X - 1004 GOTO &TURN, &CANTDO, &JUMP, &HACK, &APPLY
 1960  ON X - 1009 GOTO &CANTDO, &HIT, &SAVE, &LOAD, &HELP
 1970  ON X - 1014 GOTO &OPEN, &THROW, &FASTEN, &DIE
 1980  GOTO &CANTDO

 1990  REM *** SEMANTICS
 2000  LABEL &USE.OBJ : REM A$ = VERB, B$ = OBJECT
 2010  IF B$ = '' PRINT A$; ' WHAT?' : RETURN
 2020  LET X = CALL(&FIND.OBJ WITH A$ = B$)
 2030  IF NOT X PRINT 'YOU HAVE NO '; B$; '.'
 2040  RETURN X

 2050  LABEL &FIND.OBJ : REM A$ = OBJECT
 2060  LET R = CALL(&OBJ.ID)
 2070  IF NOT R RETURN 0
 2080  IF ABS(Z(R + 100)) = L OR Z(R + 100) = 0 RETURN R
 2090  RETURN 0

 2100  LABEL &SAVE
 2110  OPEN #3, 'SAVED' : IOCTL #3, 'TRUNC' : PRINT #3
 2120  PRINT L
 2130  LOCAL I
 2140    FOR I = 0 TO 208
 2150      PRINT Z(I)
 2160    NEXT
 2170  ENDLOC
 2180  PRINT #1 : CLOSE #3
 2190  PRINT 'SAVED.'
 2200  RETURN

 2210  LABEL &LOAD
 2220  IF CALL(&YESNO WITH P$ = 'LEAVE CURRENT GAME') THEN
 2230    OPEN #3, 'SAVED' : INPUT #3
 2240    LOCAL I
 2250      INPUT L
 2260      FOR I = 0 TO 208
 2270        INPUT Z(I)
 2280        IF EOF(0) PRINT '*** ERROR IN SAVED GAME ***' : STOP
 2290      NEXT
 2300    ENDLOC
 2310    INPUT #0 : CLOSE #3
 2320    PRINT 'LOADED.' : PRINT
 2330    GOSUB &DESCR.LONG WITH X = L
 2340    PRINT : GOSUB &INV
 2350  ENDIF
 2360  RETURN

 2370  LABEL &HELP
 2380  GOSUB &DESCR.LONG WITH X = 99
 2390  RETURN

 2400  LABEL &TAKE
 2410  IF B$ = '' PRINT 'TAKE WHAT?' : RETURN
 2420  LOCAL O
 2430    LET O = CALL(&FIND.OBJ WITH A$ = B$)
 2440    IF NOT O THEN
 2450      PRINT 'I SEE NO '; B$; ' HERE.'
 2460    ELSE
 2470      IF Z(O + 100) < 0 THEN
 2480        GOSUB &CANTDO
 2490      ELSE
 2500        IF CALL(&INV.SIZE) > 3 THEN
 2510          PRINT 'YOU ARE CARRYING TOO MUCH!'
 2520        ELSE
 2530          LET Z(O + 100) = 0
 2540          PRINT 'TAKEN.'
 2550        ENDIF
 2560      ENDIF
 2570    ENDIF
 2580  ENDLOC
 2590  RETURN

 2600  LABEL &DROP
 2610  LET X = CALL(&USE.OBJ)
 2620  IF NOT X RETURN
 2630  IF Z(X + 100) THEN
 2640    PRINT 'YOU HAVE NO '; B$; '.'
 2650  ELSE
 2660    LET Z(X + 100) = L
 2670    PRINT 'DROPPED.'
 2680  ENDIF
 2690  RETURN

 2700  LABEL &LOOK
 2710  IF B$ = '' THEN
 2720    GOSUB &DESCR.LONG WITH X = L
 2730  ELSE
 2740    LET X = CALL(&FIND.OBJ WITH A$ = B$)
 2750    RESTORE 8350
 2760    GOSUB &PRINT.DESC WITH X
 2770    IF NOT X PRINT 'I SEE NO '; B$; ' HERE.'
 2780    IF X = 3 THEN
 2790      IF Z(201) THEN
 2800        PRINT 'THE SCREEN DISPLAYS A SINGLE DIGIT,';
 2810        PRINT ' A '''; Z(0); '''.'
 2820      ELSE
 2830        PRINT 'THE COMPUTER IS OFF.'
 2840      ENDIF
 2850    ENDIF
 2860  ENDIF
 2870  RETURN

 2880  LABEL &INV
 2890  IF CALL(&INV.SIZE) THEN : REM COUNT
 2900    GOSUB &LIST.INV
 2910  ELSE
 2920    PRINT 'YOU ARE NOT CARRYING ANYTHING.'
 2930  ENDIF
 2940  RETURN

 2950  LABEL &INV.SIZE
 2960  LOCAL I
 2970    LET X = 0
 2980    FOR I = 1 TO Z(100)
 2990      IF NOT Z(I + 100) LET X = X + 1
 3000    NEXT
 3010  ENDLOC
 3020  RETURN X

 3030  LABEL &QUIT
 3040  IF RND(100) > 94 THEN
 3050    PRINT 'WINNERS NEVER QUIT. QUITTERS NEVER WIN.'
 3060  ELSE
 3070    IF CALL(&YESNO WITH P$ = 'REALLY QUIT') LET Z(0) = 0
 3080  ENDIF
 3090  RETURN

 3100  LABEL &JUMP
 3110  IF L = 25 THEN
 3120    IF Z(204) THEN
 3130      PRINT 'YOU JUMP INTO THE RIVER AND GET CARRIED AWAY BY THE'
 3140      PRINT 'UNDERGROUND TORRENT. AFTER A SHORT STRUGGLE YOU START'
 3150      PRINT 'TO DROWN. YOU PASS OUT SHORTLY AFTER RECOGNIZING THAT'
 3160      PRINT 'IT WILL NOT BE THE WATER THAT KILLS YOU.'
 3170      GOTO 1780 : REM DIE
 3180    ELSE
 3190      PRINT 'THE RIVER BED IS DRY NOW, SO THIS DOES NOT SEEM TO BE'
 3200      PRINT 'SUCH A BRILLIANT IDEA.'
 3210      RETURN
 3220    ENDIF
 3230  ENDIF
 3240  IF L = 43 THEN
 3250    PRINT 'YOU JUMP INTO THE SHAFT AND LAND ON THE RAZOR-SHARP'
 3260    PRINT 'BLADES OF THE TURBINE. WHAT A MESS!'
 3270    GOTO 1780 : REM DIE
 3280  ENDIF
 3290  PRINT 'YOU JUMP UP AND DOWN FOR A WHILE. IT WAS MORE FUN'
 3300  PRINT 'WHEN YOU WERE A CHILD.'
 3310  RETURN

 3320  LABEL &HIT
 3330  IF B$ = 'DOG' THEN
 3340    IF Z(208) LET B$ = '' : GOTO 3380
 3350    PRINT 'THERE IS NO DOG HERE.' : RETURN
 3360  ENDIF
 3370  IF B$ <> '' PRINT 'WHY SO HOSTILE?' : RETURN
 3380  LET B$ = ''

 3390  LABEL &ATTACK : REM B$ = WEAPON
 3400  IF NOT Z(208) PRINT 'ATTACK WHAT?' : RETURN
 3410  PRINT 'YOU ATTACK THE DOG';
 3420  IF B$ <> '' PRINT ' WITH YOUR '; B$;
 3430  PRINT '.'
 3440  IF RND(10) > 4 THEN
 3450    PRINT 'YOU SCARE THE DOG AWAY.'
 3460    LET Z(208) = 0
 3470  ELSE
 3480    IF B$ <> '' AND RND(10) > 4 THEN
 3490      PRINT 'YOU HIT YOURSELF WITH THE '; B$; '!';
 3500      IF RND(10) > 6 PRINT : GOTO &HELL
 3510      PRINT ' WATCH IT!'
 3520    ENDIF
 3530  ENDIF
 3540  RETURN

 3550  LABEL &HACK
 3560  IF Z(110) THEN
 3570    PRINT 'YOU DO NOT SEEM TO HAVE ANY SUITABLE TOOL.'
 3580    RETURN
 3590  ENDIF
 3600  IF Z(208) GOSUB &ATTACK WITH B$ = 'AXE' : RETURN
 3610  IF L = 15 THEN
 3620    PRINT 'YOU ARE SWINGING YOUR AXE AT THE GLASS FRONT. THE AXE'
 3630    IF RND(100) > 67 THEN
 3640      PRINT 'BOUNCES AND GETS STUCK IN YOUR HEAD.'
 3650      GOTO &HELL
 3660    ELSE
 3670      PRINT 'BOUNCES AND JUST BARELY MISSES YOUR HEAD.'
 3680    ENDIF
 3690    RETURN
 3700  ENDIF
 3710  IF L = 30 AND NOT Z(205) THEN
 3720    PRINT 'YOU PECK AWAY AT THE GROUND AND SUDDENLY IT GIVES WAY'
 3730    PRINT 'AND REVEALS A ROOM BELOW THE FLOOR.'
 3740    LET Z(205) = 1
 3750    RETURN
 3760  ENDIF
 3770  PRINT 'THAT DOES NOT SEEM TO MAKE MUCH SENSE HERE.'
 3780  RETURN

 3790  LABEL &TURN
 3800  LET X = CALL(&USE.OBJ)
 3810  IF NOT X RETURN
 3820  IF X = 3 GOTO &TURN.COMP
 3830  IF X = 13 GOTO &TURN.WHEEL
 3840  GOTO &CANTDO

 3850  LABEL &TURN.COMP
 3860  IF NOT Z(201) THEN
 3870    PRINT 'THE COMPUTER POWERS UP. A SINGLE DIGIT APPEARS ON'
 3880    PRINT 'ITS DISPLAY.'
 3890    LET Z(201) = 1
 3900  ELSE
 3910    PRINT 'BECAUSE THE COMPUTER DOES NOT REACT TO ANY INPUT,'
 3920    PRINT 'YOU DECIDE TO POWER IT OFF. THE SCREEN GOES BLANK.'
 3930    PRINT 'AND ITS HARD DISK SPINS DOWN.'
 3940    LET Z(201) = 0
 3950  ENDIF
 3960  RETURN

 3970  LABEL &TURN.WHEEL
 3980  IF Z(207) THEN
 3990    PRINT 'THE WHEEL DOES NOT MOVE A BIT.'
 4000  ELSE
 4010    IF Z(204) THEN
 4020      PRINT 'THE DISTANT ROARING GETS SOFTER AND THEN STOPS.'
 4030      LET Z(204) = 0
 4040    ELSE
 4050      PRINT 'A ROARING SOUND APPEARS IN THE DISTANCE.'
 4060      LET Z(204) = 1
 4070    ENDIF
 4080  ENDIF
 4090  RETURN

 4100  LABEL &OPEN
 4110  LET X = CALL(&USE.OBJ)
 4120  IF NOT X RETURN
 4130  IF X = 11 GOTO &OPEN.BOX
 4140  GOTO &CANTDO

 4150  LABEL &OPEN.BOX
 4160  IF Z(206) PRINT 'THE BOX IS ALREADY OPEN.' : RETURN
 4170  PRINT 'AS YOU OPEN THE BOX, AN ANGRY-LOOKING CAT EMERGES'
 4180  PRINT 'FROM IT, HISSES AT YOU, AND VANISHES IN THE DARK.'
 4190  LET Z(206) = 1
 4200  RETURN

 4210  LABEL &THROW
 4220  LET X = CALL(&USE.OBJ)
 4230  IF NOT X RETURN
 4240  IF X < 0 OR Z(X + 100) GOTO &CANTDO
 4250  IF X = 5 GOTO &THROW.BOOTS
 4260  GOTO &THROW.ANY

 4270  LABEL &THROW.BOOTS
 4280  IF Z(208) THEN
 4290    LET Z(208) = 0, Z(203) = RND(17) + 23, Z(105) = -999
 4300    PRINT 'YOU THROW THE BOOTS AT THE DOG. THE DOG SNATCHES THE'
 4310    PRINT 'BOOTS, GNAWS ON THEM, AND DISAPPEARS, OBVIOUSLY HAPPY.'
 4320  ELSE
 4330    GOTO &THROW.ANY
 4340  ENDIF
 4350  RETURN

 4360  LABEL &THROW.ANY
 4370  LET Z(X + 100) = L
 4380  PRINT 'YOU THROW THE '; B$;
 4390  IF L = 43 THEN
 4400    PRINT ' DOWN THE SHAFT';
 4410    LET Z(X + 100) = 44
 4420  ENDIF
 4430  PRINT '.' : RETURN

 4440  LABEL &FASTEN
 4450  LET X = CALL(&USE.OBJ)
 4460  IF NOT X RETURN
 4470  IF X = 7 GOTO &FASTEN.ROPE
 4480  GOTO &CANTDO

 4490  LABEL &FASTEN.ROPE
 4500  IF L = 43 THEN
 4510    IF Z(107) = -43 THEN
 4520      PRINT 'THE ROPE IS ALREADY FIRMLY IN PLACE.'
 4530    ELSE
 4540      PRINT 'YOU ATTACH THE ROPE TO A STEEL ROD IN THE WALL OF'
 4550      PRINT 'THE RIVER BED.'
 4560      LET Z(107) = -43
 4570    ENDIF
 4580    RETURN
 4590  ENDIF
 4600  PRINT 'YOU FIND NO PLACE TO ATTACH THE ROPE HERE.'
 4610  RETURN

 4620  LABEL &APPLY
 4630  LET X = CALL(&USE.OBJ)
 4640  IF NOT X RETURN
 4650  IF X = 2 GOTO &USE.OIL
 4660  IF X = 3 GOTO &USE.COMP
 4670  IF X = 4 GOSUB &ATTACK WITH B$ = 'KNIFE' : RETURN
 4680  IF X = 5 GOTO &USE.BOOTS
 4690  IF X = 12 GOTO &USE.RAFT
 4700  PRINT 'HOW DO YOU DO THIS?' : RETURN

 4710  LABEL &USE.COMP
 4720  PRINT 'THE COMPUTER DOES NOT REACT TO ANY INPUT.'
 4730  RETURN

 4740  LABEL &USE.BOOTS
 4750  PRINT 'THE BOOTS ARE MUCH TOO LARGE TO WEAR!'
 4760  RETURN

 4770  LABEL &USE.OIL
 4780  IF L = 40 THEN
 4790    PRINT 'YOU APPLY SOME OIL TO THE CONTROL WHEEL.'
 4800    LET Z(207) = 0
 4810  ELSE
 4820    PRINT 'YOU SPILL SOME OIL. WATCH IT! YOU MAY SLIP ON IT LATER!'
 4830  ENDIF
 4840  RETURN

 4850  LABEL &USE.RAFT
 4860  LET Z(112) = -999
 4870  PRINT 'YOU PUSH THE RAFT INTO THE RIVER AND JUMP ABOARD. THE'
 4880  PRINT 'RAFT TUMBLES DOWN THE TORRENT, PASSES UNDER A BRIDGE,'
 4890  PRINT 'AND FINALLY REACHES A HOLE IN THE CONCRETE WALL OF THE'
 4900  PRINT 'RIVER BED. THE HOLE IS HIGH UP IN THE WALL. YOU CAN'
 4910  PRINT 'PROBABLY REACH IT, BUT THEN THE RAFT WILL BE LOST!'
 4920  PRINT
 4930  IF CALL(&YESNO WITH P$ = 'REACH FOR THE HOLE') THEN
 4940    PRINT 'YOU JUMP AND GRAB HOLD OF THE HOLE, WHICH TURNS OUT TO'
 4950    PRINT 'BE THE ENTRANCE OF A TUNNEL.'
 4960    PRINT
 4970    GOSUB &TRAVEL WITH L1 = 39
 4980    RETURN
 4990  ELSE
 5000    PRINT 'YOU CONTINUE YOUR JOURNEY ON THE RAFT. SHORTLY AFTER'
 5010    PRINT 'PASSING THE HOLE, THE RIVER MAKES A SHARP BEND *DOWN*'
 5020    PRINT 'AND FALLS INTO A SHAFT. YOU AND THE RAFT FALL DOWN THE'
 5030    PRINT 'SHAFT.'
 5040    PRINT 'AT THE BOTTOM OF THE SHAFT THERE IS A HUGE TURBINE.'
 5050    PRINT 'THE WATER PRESSURE CAUSES YOU TO BLACK OUT, THOUGH,'
 5060    PRINT 'SO YOU DO NOT NOTICE HOW THE RAZOR-SHARP BLADES CUT'
 5070    PRINT 'YOUR BODY TO SHREDS.'
 5080    GOTO &HELL
 5090  ENDIF
 5100  RETURN

 5110  LABEL &DIE
 5120  IF CALL(&YESNO WITH P$ = 'ARE YOU SURE') GOTO &HELL
 5130  RETURN
 5140  RETURN

 5150  LABEL &YESNO : REM P$ = PROMPT
 5160  LOCAL A$
 5170    REPEAT
 5180      PRINT P$; '? '; : INPUT A$
 5190      LET A$ = MID$(A$, 1, 1)
 5200      IF A$ <> 'Y' AND A$ <> 'N' PRINT 'YES OR NO?'
 5210    UNTIL A$ = 'Y' OR A$ = 'N'
 5220    LET X = A$ = 'Y'
 5230  ENDLOC
 5240  PRINT : RETURN X

 5250  LABEL &CANTDO
 5260  ON RND(5) + 1 GOTO 5270, 5280, 5290, 5300, 5310
 5270  PRINT 'YOU CANNOT DO THIS.' : RETURN
 5280  PRINT 'YOU''RE NOT SERIOUS!' : RETURN
 5290  PRINT 'THIS IS IMPOSSIBLE.' : RETURN
 5300  PRINT 'NO.' : RETURN
 5310  PRINT 'THINK AGAIN!' : RETURN

 5320  LABEL &PARDON
 5330  ON RND(5) + 1 GOTO 5340, 5350, 5360, 5370, 5380
 5340  PRINT 'WHAT?' : RETURN
 5350  PRINT 'PARDON?' : RETURN
 5360  PRINT 'I DON''T UNDERSTAND.' : RETURN
 5370  PRINT 'CAN''T PARSE THIS!' : RETURN
 5380  PRINT 'I AM NOT SURE WHAT YOU MEAN.' : RETURN

 5390  LABEL &INPUT
 5400  PRINT '* '; : INPUT A$
 5410  LET B$ = ''
 5420  LOCAL I
 5430    LET I = POS(' ', A$)
 5440    IF I THEN
 5450      LET A$(1) = MID$(A$, I)
 5460      LET A$(0) = MID$(A$, 1, I - 1)
 5470      GOSUB &RMBLANK
 5480    ENDIF
 5490  ENDLOC
 5500  RETURN

 5510  LABEL &RMBLANK : REM FROM B$
 5520  LOCAL I, C$, T$ = ''
 5530    FOR I = 1 TO LEN(B$)
 5540      LET C$ = MID$(B$, I, 1)
 5550      IF C$ <> ' ' LET T$ = T$ + C$
 5560    NEXT
 5570    LET B$ = T$
 5580  ENDLOC
 5590  RETURN

 5600  LABEL &FIND.PATH : REM X = VERB
 5610  RESTORE 6930
 5620  LOCAL D = 0
 5630    REPEAT
 5640      GOSUB &READ.MAP
 5650      IF T(0) = L AND CALL(&REACHABLE WITH X) LET D = T(1), D1 = T(2)
 5660    UNTIL T(0) = -1
 5670    LET X = D
 5680  ENDLOC
 5690  RETURN X

 5700  LABEL &REACHABLE : REM X = VERB
 5710  LOCAL I = 2
 5720    WHILE I < 10 AND T(I) AND T(I) <> X
 5730      LET I = I + 1
 5740    WEND
 5750    LET X = T(I)
 5760  ENDLOC
 5770  RETURN X

 5780  LABEL &READ.MAP
 5790  LOCAL I = 0, X
 5800    REPEAT
 5810      READ X
 5820      LET T(I) = X : LET I = I + 1
 5830    UNTIL NOT X OR X = -1 OR I >= 10
 5840  ENDLOC
 5850  RETURN

 5860  LABEL &FIND.VERB : REM A$ = VERB
 5870  RESTORE 8580
 5880  RETURN CALL(5910)

 5890  LABEL &OBJ.ID : REM A$ = OBJECT
 5900  RESTORE 8180
 5910  LOCAL I, W$, A$
 5920    LET A$ = MID$(A$, 1, 7), X = 0
 5930    READ I
 5940    WHILE I
 5950      READ W$
 5960      IF A$ = MID$(W$, 1, 7) LET X = I
 5970      READ I
 5980    WEND
 5990  ENDLOC
 6000  RETURN X

 6010  LABEL &DESCRIBE : REM X = LOCATION
 6020  IF NOT Z(X) GOTO &DESCR.LONG
 6030  RESTORE 7830
 6040  LET R = CALL(&PRINT.DESC WITH X)
 6050  IF R GOSUB &LIST.OBJS : RETURN

 6060  LABEL &DESCR.LONG
 6070  GOSUB &PRT.XDESC WITH X
 6080  GOSUB &LIST.OBJS
 6090  IF L = 30 AND Z(205) THEN
 6100    PRINT 'THERE IS AN OPENING IN THE FLOOR.'
 6110  ENDIF
 6120  IF (L = 15 OR L = 25) AND Z(204) THEN
 6130    PRINT 'IN THE CONCRETE BED A RIVER THUNDERS TO THE NORTH.'
 6140  ENDIF
 6150  IF L = 37 THEN
 6160    IF Z(204) THEN
 6170      PRINT 'A TORRENT THUNDERS IN A RIVER BED TO THE EAST.'
 6180    ELSE
 6190      PRINT 'THERE IS A DRY RIVER BED TO THE EAST.'
 6200    ENDIF
 6210  ENDIF
 6220  LET Z(X) = 1
 6230  RETURN

 6240  LABEL &PRINT.DESC : REM X = OBJECT/LOCATION
 6250  LOCAL I, A$, P = 0
 6260    READ I
 6270    WHILE I AND I <= X
 6280      READ A$
 6290      IF X = I PRINT A$ : LET P = 1
 6300      READ I
 6310    WEND
 6320  RETURN P

 6330  LABEL &PRT.XDESC : REM X = OBJECT/LOCATION
 6340  LOCAL I, J, A$, L$
 6350    OPEN #3, 'NUCLEUS.DAT' : INPUT #3
 6360    INPUT L$
 6370    LET I = VAL(L$)
 6380    WHILE NOT EOF(0) AND I AND I <= X
 6390      LET J = POS(' ', L$), A$ = L$
 6400      IF J LET A$ = MID$(L$, J + 1)
 6410      IF X = I PRINT A$
 6420      INPUT L$
 6430      LET I = VAL(L$)
 6440    WEND
 6450    INPUT #0 : CLOSE #3
 6460  RETURN

 6470  LABEL &LIST.OBJS
 6480  LOCAL C$, I
 6490    FOR I = 1 TO Z(100)
 6500      IF L = ABS(Z(I + 100)) THEN
 6510        PRINT 'THERE IS';
 6520        GOSUB &OBJ.NAME WITH X = I
 6530        IF MID$(X$, 1, 3) <> 'THE' PRINT ' A';
 6540        LET C$ = MID$(X$, 1, 1)
 6550        IF POS(C$, 'AEIOU') PRINT 'N';
 6560        PRINT ' '; X$; ' HERE.'
 6570      ENDIF
 6580    NEXT
 6590  ENDLOC
 6600  RETURN

 6610  LABEL &LIST.INV
 6620  LOCAL C$, I, F = 1, C = 0
 6630    PRINT 'YOU ARE CARRYING: ';
 6640    FOR I = 1 TO Z(100)
 6650      IF NOT Z(I + 100) THEN
 6660        GOSUB &OBJ.NAME WITH X = I
 6670        LET C$ = MID$(X$, 1, 1)
 6680        IF NOT F PRINT ', ';
 6690        LET C = C + 1 : IF C > 3 LET C = 0 : PRINT
 6700        PRINT 'A';
 6710        IF POS(C$, 'AEIOU') PRINT 'N';
 6720        PRINT ' '; X$;
 6730        LET F = 0
 6740      ENDIF
 6750    NEXT
 6760    PRINT '.'
 6770  ENDLOC
 6780  RETURN

 6790  LABEL &OBJ.NAME
 6800  REM *** X = OBJECT ID; RETURN NAME IN X$
 6810  RESTORE 8180
 6820  LET X$ = ''
 6830  LOCAL I, A$
 6840    READ I
 6850    WHILE I AND I <= X
 6860      READ A$
 6870      IF X = I AND X$ = '' LET X$ = A$
 6880      READ I
 6890    WEND
 6900  ENDLOC
 6910  RETURN

 6920  REM *** MAP
 6930  DATA 1, 2, 5, 15, 0
 6940  DATA 2, 1, 7, 14, 27, 0
 6950  DATA 2, 4, 6, 0
 6960  DATA 4, 2, 8, 0
 6970  DATA 4, 3, 7, 0
 6980  DATA 3, 4, 5, 0
 6990  DATA 3, 1, 8, 14, 27, 0
 7000  DATA 1, 3, 6, 15, 0
 7010  DATA 1, 10, 2, 20, 0
 7020  DATA 10, 1, 4, 14, 27, 0
 7030  DATA 2, 5, 1, 16, 0
 7040  DATA 5, 2, 3, 15, 0
 7050  DATA 4, 7, 2, 17, 0
 7060  DATA 7, 4, 4, 15, 0
 7070  DATA 3, 8, 3, 18, 0
 7080  DATA 8, 3, 1, 15, 0
 7090  DATA 8, 9, 2, 19, 0
 7100  DATA 9, 8, 4, 18, 0
 7110  DATA 5, 6, 13, 1010, 0
 7120  DATA 6, 5, 16, 9, 0
 7130  DATA 10, -11, -12, 10, 11, 0
 7140  DATA 12, -11, -10, 9, 18, 0
 7150  DATA 12, 13, 2, 15, 24, 0
 7160  DATA 13, 12, 20, 4, 0
 7170  DATA 13, 14, 2, 21, 27, 0
 7180  DATA 14, 13, 4, 24, 0
 7190  DATA 14, 15, 2, 22, 0
 7200  DATA 15, 14, 4, 21, 27, 0
 7210  DATA 14, 16, 8, 23, 0
 7220  DATA 16, 14, 6, 21, 27, 0
 7230  DATA 14, 17, 5, 0
 7240  DATA 17, 14, 7, 21, 27, 0
 7250  DATA 14, 18, 6, 0
 7260  DATA 18, 14, 8, 21, 27, 0
 7270  DATA 14, 21, 7, 0
 7280  DATA 21, 14, 5, 21, 27, 0
 7290  DATA 14, 22, 1, 0
 7300  DATA 22, 17, 3, 0
 7310  DATA 14, 23, 3, 25, 0
 7320  DATA 23, 14, 1, 27, 21, 0
 7330  DATA 23, 24, 15, 3, 0
 7340  DATA 24, 23, 25, 1, 0
 7350  DATA 24, 25, 2, 26, 0
 7360  DATA 25, 24, 4, 0
 7370  DATA 25, 26, 2, 0
 7380  DATA 26, 25, 26, 4, 0
 7390  DATA 26, 27, 6, 28, 27, 0
 7400  DATA 27, 26, 8, 15, 0
 7410  DATA 27, 28, 4, 29, 0
 7420  DATA 28, 27, 2, 27, 28, 0
 7430  DATA 27, 29, 9, 1006, 30, 31, 0
 7440  DATA 29, 27, 10, 27, 28, 1007, 0
 7450  DATA 24, 30, 3, 32, 0
 7460  DATA 30, 24, 1, 0
 7470  DATA 30, 31, 4, 33, 0
 7480  DATA 31, 30, 2, 32, 0
 7490  DATA 31, 32, 3, 0
 7500  DATA 32, 31, 1, 33, 15, 0
 7510  DATA 31, 33, 1, 0
 7520  DATA 33, 31, 3, 33, 15, 0
 7530  DATA 33, -35, -34, 1, 34, 9, 1006, 0
 7540  DATA 34, 33, 3, 33, 10, 0
 7550  DATA 34, 18, 5, 0
 7560  DATA 30, 36, 1006, 10, 35, 0
 7570  DATA 36, 30, 9, 32, 0
 7580  DATA 36, 37, 2, 36, 0
 7590  DATA 37, 36, 35, 4, 0
 7600  DATA 37, 38, 2, 37, 0
 7610  DATA 38, 37, 4, 36, 0
 7620  DATA 39, 38, 4, 37, 1007, 0
 7630  DATA 39, 40, 6, 38, 0
 7640  DATA 40, 39, 8, 29, 0
 7650  DATA 40, 41, 4, 27, 41, 0
 7660  DATA 41, 40, 2, 38, 0
 7670  DATA 41, -42, -26, 3, 39, 0
 7680  DATA 38, 43, 1, 40, 39, 0
 7690  DATA 43, 38, 3, 37, 0
 7700  DATA 43, -50, -44, 1006, 10, 0
 7710  DATA 44, -50, -43, 1006, 9, 0
 7720  DATA 44, 49, 42, 1, 0
 7730  DATA 49, 44, 3, 36, 0
 7740  DATA 49, 45, 1, 44, 9, 0
 7750  DATA 45, 49, 3, 42, 0
 7760  DATA 45, 46, 10, 43, 0
 7770  DATA 46, 45, 9, 44, 0
 7780  DATA 46, -47, -10, 20, 0
 7790  DATA 10, 48, 9, 12, 0
 7800  DATA 12, 48, 12, 0
 7810  DATA -1

 7820  REM *** SHORT DESCRIPTIONS OF LOCATIONS
 7830  DATA 1, 'MAIN HALL'
 7840  DATA 2, 'CORRIDOR'
 7850  DATA 3, 'CORRIDOR'
 7860  DATA 4, 'CORRIDOR'
 7870  DATA 5, 'RESTROOM'
 7880  DATA 7, 'LECTURE ROOM'
 7890  DATA 8, 'LAB'
 7900  DATA 9, 'CLOSET'
 7910  DATA 10, 'ELEVATOR CABIN'
 7920  DATA 12, 'ELEVATOR CABIN'
 7930  DATA 13, 'LONG CORRIDOR'
 7940  DATA 14, 'ROUND HALL'
 7950  DATA 15, 'RIVER VIEW'
 7960  DATA 16, 'SUPPLY ROOM'
 7970  DATA 22, 'HYPERSPACE'
 7980  DATA 23, 'THE RUSTY GRATE SQUEAKS AS YOU WALK OVER IT'
 7990  DATA 25, 'RIVER BRIDGE'
 8000  DATA 27, 'CONSTRUCTION HALL'
 8010  DATA 29, 'DRILLING MACHINE'
 8020  DATA 30, 'HOLLOW ROOM'
 8030  DATA 31, 'QUARTERS'
 8040  DATA 34, 'VENTILATION SHAFT'
 8050  DATA 36, 'MAINTENANCE ROOM'
 8060  DATA 37, 'FLOOD BASIN'
 8070  DATA 38, 'RIVER BED'
 8080  DATA 39, 'TUNNEL'
 8090  DATA 40, 'CONTROL ROOM'
 8100  DATA 41, 'BATTERY HALL'
 8110  DATA 43, 'TURBINE SHAFT'
 8120  DATA 44, 'CONCRETE BASIN'
 8130  DATA 45, 'TOP OF ''TANK'''
 8140  DATA 46, 'AIRLOCK'
 8150  DATA 49, 'IDYLLIC VALLEY'
 8160  DATA 0

 8170  REM *** OBJECT NAMES
 8180  DATA 1, 'KEY CARD', 1, 'KEYCARD', 1, 'CARD'
 8190  DATA 2, 'OIL CAN', 2, 'OILCAN', 2, 'OIL', 2, 'CAN'
 8200  DATA 3, 'COMPUTER'
 8210  DATA 4, 'KNIFE'
 8220  DATA 5, 'PAIR OF RUBBER BOOTS', 5, 'RUBBERB', 5, 'BOOTS'
 8230  DATA 5, 'BOOT'
 8240  DATA 6, 'THE USUAL SCRIBBLING', 6, 'GRAFFIT', 6, 'WRITING'
 8250  DATA 6, 'SCRIBBL'
 8260  DATA 7, 'ROPE'
 8270  DATA 8, 'SIGN'
 8280  DATA 9, 'KEY', 9, 'KEYS'
 8290  DATA 10, 'PICK-AXE', 10, 'AXE'
 8300  DATA 11, 'BOX'
 8310  DATA 12, 'RAFT', 12, 'BOAT'
 8320  DATA 13, 'CONTROL WHEEL', 13, 'WHEEL'
 8330  DATA 0

 8340  REM *** OBJECT DESCRIPTIONS
 8350  DATA 1, 'A SMALL MAGNETIC CARD THAT CAN BE USED TO OPEN'
 8360  DATA 1, 'DOORS, ETC.'
 8370  DATA 2, 'A SMALL CAN THAT IS FILLED WITH OIL. IT DRIPS SLIGHTLY.'
 8380  DATA 3, 'A STANDARD COMPUTER TERMINAL SITTING ON A DESK.'
 8390  DATA 3, 'IT DOES NOT REACT TO ANY INPUT, BUT ITS PROMINENT'
 8400  DATA 3, 'LOCATION MAKES IT LOOK IMPORTANT.'
 8410  DATA 4, 'A NASTY-LOOKING COMBAT KNIFE.'
 8420  DATA 5, 'A PAIR OF YELLOW RUBBER BOOTS.'
 8430  DATA 6, 'YOU DECYPHER THE SCRIBBLING ON THE WALL:'
 8440  DATA 6, ''
 8450  DATA 6, 'BEWARE OF THE TURING TARPIT!'
 8460  DATA 6, 'FUCK THIS WORK!!#@@#&'
 8470  DATA 6, '3 + 1 = 4'
 8480  DATA 6, 'INSERT RANDOM OBSCENITY'
 8490  DATA 7, 'A LONG, STURDY NYLON ROPE.'
 8500  DATA 8, 'THE SIGN READS: ''IS THE COMPUTER STILL ON?'''
 8510  DATA 9, 'A STANDARD DOOR KEY.'
 8520  DATA 10, 'A WOODEN HEAVY-DUTY PICK-AXE WITH A STEEL BLADE.'
 8530  DATA 11, 'A TRANSPORT BOX FOR MEDIUM-SIZED ANIMALS.'
 8540  DATA 12, 'A LARGE RUBBER BOAT WITH A WOODEN BOTTOM. THERE ARE'
 8550  DATA 12, 'NO OARS AND THE MOTOR SEEMS TO BE BROKEN.'
 8560  DATA 0

 8570  REM *** VERBS
 8580  DATA 1, 'NORTH', 1, 'N'
 8590  DATA 2, 'EAST', 2, 'E'
 8600  DATA 3, 'SOUTH', 3, 'S'
 8610  DATA 4, 'WEST', 4, 'W'
 8620  DATA 5, 'NORTH-E', 5, 'NE'
 8630  DATA 6, 'SOUTH-E', 6, 'SE'
 8640  DATA 7, 'SOUTH-W', 7, 'SW'
 8650  DATA 8, 'NORTH-W', 8, 'NW'
 8660  DATA 9, 'UP', 9, 'U'
 8670  DATA 10, 'DOWN', 10, 'D'
 8680  DATA 11, 'FACILIT'
 8690  DATA 12, 'ROOF'
 8700  DATA 13, 'COMPART'
 8710  DATA 14, 'MAINHAL', 14, 'MAIN'
 8720  DATA 15, 'CORRIDO', 15, 'CORR'
 8730  DATA 16, 'RESTROO', 16, 'TOILET'
 8740  DATA 17, 'LECTURE'
 8750  DATA 18, 'LABORAT', 18, 'LAB'
 8760  DATA 19, 'CLOSET'
 8770  DATA 20, 'LIFT', 20, 'ELEVATO'
 8780  DATA 21, 'ROUND', 21, 'ROUNDHA'
 8790  DATA 22, 'RIVER', 22, 'RIVERVI'
 8800  DATA 23, 'SUPPLY', 23, 'SUPPLYR'
 8810  DATA 24, 'LONG', 24, 'LONGCOR'
 8820  DATA 25, 'RUSTY', 25, 'RUSTYGR'
 8830  DATA 26, 'BRIDGE', 26, 'RIVERBR'
 8840  DATA 27, 'HALL'
 8850  DATA 28, 'CONSTRU'
 8860  DATA 29, 'TUNNEL'
 8870  DATA 30, 'DRILLIN'
 8880  DATA 31, 'MACHINE'
 8890  DATA 32, 'HOLLOW', 32, 'HOLLOWR'
 8900  DATA 33, 'QUARTERS'
 8910  DATA 34, 'VENTILA', 34, 'SHAFT'
 8920  DATA 35, 'MAINTEN'
 8930  DATA 36, 'BASIN', 36, 'FLOODBA'
 8940  DATA 37, 'RIVER', 37, 'RIVERBE'
 8950  DATA 38, 'CONTROL'
 8960  DATA 39, 'SHAFT'
 8970  DATA 40, 'TURBINE'
 8980  DATA 41, 'BATTERY'
 8990  DATA 42, 'VALLEY', 42, 'IDYLLIC'
 9000  DATA 43, 'AIRLOCK'
 9010  DATA 44, 'TANK'
 9020  DATA 1000, 'TAKE', 1000, 'GET', 1000, 'FETCH'
 9030  DATA 1001, 'DROP'
 9040  DATA 1002, 'LOOK', 1002, 'EXAMINE'
 9050  DATA 1003, 'BYE', 1003, 'QUIT'
 9060  DATA 1004, 'INVENTO', 1004, 'LIST', 1004, 'INV'
 9070  DATA 1005, 'TURN', 1005, 'SPIN', 1005, 'ROTATE'
 9080  DATA 1006, 'CLIMB'
 9090  DATA 1007, 'JUMP'
 9100  DATA 1008, 'HACK', 1008, 'PECK'
 9110  DATA 1009, 'APPLY', 1009, 'USE'
 9120  DATA 1010, 'SIT'
 9130  DATA 1011, 'ATTACK', 1011, 'HIT', 1011, 'KICK'
 9140  DATA 1012, 'SAVE'
 9150  DATA 1013, 'BACK', 1013, 'LOAD'
 9160  DATA 1014, 'HELP', 1014, '?'
 9170  DATA 1015, 'OPEN'
 9180  DATA 1016, 'THROW', 1016, 'TOSS', 1016, 'CAST'
 9190  DATA 1017, 'FASTEN', 1017, 'ATTACH'
 9200  DATA 1018, 'DIE'
 9210  DATA 0
