	.TITLE	S3ADRV1 - Loadable driver extensions for S3 ViRGE display

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.PROC	80486
	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSX.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\XOSDOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSTRM.PAR
	.INCLUD	XOSINC:\XMAC\XOSXTRM.PAR
	.INCLUD	XOSINC:\XMAC\PCAT.PAR
	.INCLUD	XOSINC:\XMAC\XOSLKE.PAR
	.INCLUD	XOSINC:\XMAC\XOSXLKE.PAR
	.INCLUD	VGAADRV.PAR

	LKECONT
	CODE

;Subroutine to do extended set up for addunit
;	c[EDI] = Offset of DHDB
;	CALL	vgaxsetup
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal

vgaxsetup::
	MOVL	EDX, dhdb_bioreg[EDI]	;Get port for the CRT controller index
	ADDL	EDX, #P_EGACCRTINDX-P_EGABASE.B
	MOVB	AL, #38h		;Make sure all registers are unlocked
	OUTB	[DX]
	INCL	EDX
	MOVB	AL, #48h
	OUTB	[DX]
	DECL	EDX
	MOVB	AL, #39h
	OUTB	[DX]
	INCL	EDX
	MOVB	AL, #0A0h
	OUTB	[DX]
	DECL	EDX
	MOVB	AL, #35h
	OUTB	[DX]
	INCL	EDX
	MOVB	AL, #0
	OUTB	[DX]
	DECL	EDX
	MOVB	AL, #59h		;Get the PCI base address
	OUTB	[DX]
	INCL	EDX
	INB	[DX]
	DECL	EDX
	MOVB	AH, AL
	MOVB	AL, #5Ah
	OUTB	[DX]
	INCL	EDX
	INB	[DX]
	DECL	EDX
	SHLL	EAX, #16t
	MOVL	dhdb_pgaddr.B[EDI], EAX
	MOVL	dhdb_ptaddr.B[EDI], #0A0000h


;;;	MOVB	AL, #66h		;Enable extended functions
;;;	OUTB	[DX]
;;;	INCL	EDX
;;;	INB	[DX]
;;;	ORB	AL, #01h
;;;	OUTB	[DX]
;;;	DECL	EDX
;;;	MOVB	AL, #53h		;Enable linear addressing
;;;	OUTB	[DX]
;;;	INCL	EDX
;;;	MOVB	AL, #08h
;;;	OUTB	[DX]

	PUSHL	EDI
	MOVL	EDI, #pgtable+{la_graphdisp>10t}
	PUSHL	DS			;Clear any previous graphics mapping
	POPL	ES
	CLRL	EAX
	MOVL	ECX, #800000h
	CLD
	IFFAULT	10$
	RSTOSL	[EDI]
	FAULTHDR
10$:	POPL	EDI
	MOVL	ECX, #4112t		;Get number of pages to map (16M + 64K)
	MOVL	EBX, #la_graphdisp	;Get linear address for screen buffer
	MOVL	EDX, dhdb_pgaddr.B[EDI]	;Get physical address
	PUSHL	EDI
	MOVL	EDI, #-1
	MOVL	EAX, #27h
	CALL	knlMapPhyPages##	;Map the pages
	POPL	EDI
	JC	14$			;If error
	MOVL	dhdb_vgaddr.B[EDI], #graphdisp
14$:	RET

	END
