	.TITLE	wildcmp - Function to do wild-card compare

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.PROC	80386

	.INCLUD	XOSINC:\XMAC\STDIO.PAR
	.INCLUD	XOSINC:\XMAC\CTYPE.PAR
	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\XOSTIME.PAR

	.STDSEGS

	.PSECT	_TEXT_p

;Function to set do wild card compare
;	long rtn = wildcmp(
;	    char *str1		// String 1 (may contain wild card characters
;	    char *str2		// String 2
;	    long  flags);	// Bit 0 = 0 for mixed case match, 1 for exact
;				//   case match, other bits must be 0
arg_str1 =!16t
arg_str2 =!20t
arg_flags=!24t

$$$=!0
FRM wc_svESI, 4t
FRM wc_svEDI, 4t
wc_SIZE=!$$$

	.ENTRY	wildcmp
wildcmp::
	PUSHL	EDI
	PUSHL	ESI	
	ENTER	wc_SIZE, 0		;Setup our stack frame
	MOVL	ESI, arg_str1.B[EBP]
	MOVL	EDI, arg_str2.B[EBP]
	CLD
2$:	CALL	cmpstr			;Compare strings
	JNC	16$.S			;If have complete match
	JNE	12$.S			;If match not possible
4$:	MOVB	AL, [ESI]		;Here with *
6$:	MOVB	AH, [EDI]		;Scan to first match
	INCL	EDI
	CMPB	AH, AL
	JE	18$.S			;If match
	TESTB	arg_flags.B[EBP], #01h	;Want exact case match?
	JNE	10$.S			;Yes
	XORB	AL, AH			;No - might be different case match?
	TESTB	AL, #~20h
	JNE	8$.S			;No
	XORB	AL, AH			;Yes
	MOVZBL	EDX, AL
	BTL	CS:ltrtbl, EDX		;Is it a letter?
	JC	18$.S			;Yes - match
	JMP	10$.S			;No - not match

8$:	XORB	AL, AH
10$:	TESTB	AH, AH			;Not match - end?
	JNE	6$.S			;No - continue
12$:	MOVL	EAX, #1			;Yes - indicate no match
14$:	LEAVE				;And return
	POPL	ESI
	POPL	EDI
	RET

;Here with a complete match

16$:	CLRL	EAX
	JMP	14$.S

;Here with match after *

18$:	TESTB	AL, AL			;Found match if end of string here
	JE	16$.S
	MOVL	wc_svESI.B[EBP], ESI	;Save state
	MOVL	wc_svEDI.B[EBP], EDI
	INCL	ESI
	CALL	cmpstr			;Compare strings
	JNC	16$.S			;If have complete match
	JE	4$.S			;If found another *
	MOVL	EDI, wc_svEDI.B[EBP]	;Match not possible here - advance one
	MOVL	ESI, wc_svESI.B[EBP]	;  character and try to match that
					;  string
	JMP	4$.S			;Continue
.PAGE
	.SBTTL	cmpstr - Subroutine to compare strings

;Subroutine to compare strings (string 1 may contain wild characters) up to
;  wild card character
;	c{ESI} = Pointer to string 1
;	c{EDI} = Pointer to string 2
;	CALL	cmpstr
;	C:clr,Z:set = Complete match
;	C:set,Z:set = * encountered
;	C:set,Z:clr = No match possible

cmpstr:	LODSB	[ESI]			;Anything left in string 1?
	TESTB	AL, AL
	JNE	6$.S			;Yes
	CMPB	[EDI], #0		;No - anything left in string 2?
	JE	4$.S			;No - have complete match
2$:	TESTL	ESP, ESP		;Yes - make sure Z is clear to indicate
					;  no possible match
	STC				;And set C
4$:	RET

;Here with more in string 2 to check

6$:	CMPB	AL, #'*'		;Another wild character?
	JE	18$.S			;Yes
	CMPB	AL, #'?'		;No - match any single character?
	JE	12$.S			;Yes
8$:	CMPB	[EDI], #0		;No - end of string 2?
	JE	2$.S			;Yes - go check some more
	MOVB	AH, [EDI]		;No - compare
	INCL	EDI
	CMPB	AH, AL
	JE	cmpstr.S		;If exact match
	TESTB	arg_flags.B[EBP], #01h	;Want exact case match?
	JNE	2$.S			;Yes
	XORB	AH, AL			;No - might be different case match?
	TESTB	AH, #~20h
	JNE	2$.S			;No
	MOVZBL	EDX, AL
	BTL	CS:ltrtbl, EDX		;Yes - is it a letter?
	JC	cmpstr.S		;Yes - match
	JMP	2$.S			;No - not match

;Here with ? in string 1

12$:	CMPB	[EDI], #0		;End of string 2?
	JE	cmpstr.S		;Yes - continue
16$:	INCL	EDI
	JMP	cmpstr.S		;And continue

;Here with * in string 1

18$:	CLRL	EAX			;Indicate have *
	STC
	RET

	.MOD	4
ltrtbl:	.LONG	00000000
	.LONG	00000000
	.LONG	07FFFFFE
	.LONG	07FFFFFE
	.LONG	00000000
	.LONG	00000000
	.LONG	00000000
	.LONG	00000000

	.END
