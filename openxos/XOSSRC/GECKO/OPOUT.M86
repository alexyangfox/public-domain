	.SBTTL	OPOUT - Opcode output routines for GECKO86

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

;Define values for the rgopsz byte

SZ_OPR=!0t		;Use size of operand for register size
SZ_8  =!2t		;8 bit register
SZ_16 =!4t		;16 bit register
SZ_32 =!6t		;32 bit register

	.SBTTL	outopc - Subroutine to output value as instruction

;Subroutine to output value as instruction
;	CALL	outopc
;	C:clr = Normal
;	C:set = Error

outopc:	MOVL	EBX, opnofs		;Get first byte
	MOVL	opxofs, EBX
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	MOVB	value, AL		;Save first byte
	CALL	getmtype		;See if 16 or 32 bit segment
	SHRB	AL, #3
	MOVB	adrsz16, AL
	MOVB	oprsz16, AL
	MOVB	rgopsz, #SZ_OPR
	MOVB	reppfx, #0		;No repeat prefix yet
	MOVB	segpfx, #0		;No segment prefix yet
	MOVB	opnsiz, #0		;Clear length of instruction
outop2:	MOVB	oofetch, #0		;Indicate no additional bytes yet
	MOVL	ESI, #doptbl		;Point to start of opcode display
	MOVL	ECX, #DOPTSIZ		;  table
2$:	MOVB	AL, value		;Get byte we are searching for
	XORB	AL, CS:opo_byt0.B[ESI]	;Combine with table value
	ANDB	AL, CS:opo_msk0.B[ESI]	;Mask out don't care bits
	JNE	outonx.S		;No match
	CMPB	CS:opo_msk1.B[ESI], #0	;Do we care about the next byte?
	JE	10$			;No - we have a full match
	CMPB	oofetch, #1		;Yes - have we fetched it yet?
	JAE	6$.S			;Yes
	INCW	BX			;No - bump address
	INCB	oofetch
	CALL	getmbyte		;Get next byte
	JC	hvaerr.S
	MOVB	value+1, AL		;Save second byte
6$:	MOVB	AL, value+1		;Get byte we are searching for
	XORB	AL, CS:opo_byt1.B[ESI]	;Combine with table value
	ANDB	AL, CS:opo_msk1.B[ESI]	;Mask out don`t care bits
	JNE	outonx.S		;No match - continue
	CMPB	CS:opo_msk2.B[ESI], #0	;Do we care about the next byte?
	JE	10$.S			;No - we have a full match
	CMPB	oofetch, #2		;Yes - have we fetched it yet?
	JAE	8$.S			;Yes
	INCL	EBX			;No - bump address
	INCB	oofetch
	CALL	getmbyte		;Get next byte
	JC	hvaerr.S
	MOVB	value+2, AL		;Save second byte
8$:	MOVB	AL, value+2		;Get byte we are searching for
	XORB	AL, CS:opo_byt2.B[ESI]	;Combine with table value
	ANDB	AL, CS:opo_msk2.B[ESI]	;Mask out don't care bits
	JE	10$.S			;If have a full match
outonx:	ADDL	ESI, #opo_ENSZ.B	;Not this one - bump pointer
	LOOP	ECX, 2$			;Continue if more to check
badopc:	MOVB	validi, #0		;Indicate not valid instruction
	MOVL	EBX, #bdomsg		;Bad opcode
	CALL	outstrc
	MOVB	AL, value		;Display the opcode byte
	CALL	outby0			;And return	
	MOVB	opnsiz, #1		;Treat it as 1 byte long
	CLC
	RET

;Here with full match

10$:	JMPIW	CS:opo_disp.B[ESI]	;Dispatch to output routine

;Here with memory access error

hvaerr:	MOVL	EBX, #adrmsg
	CALL	outstrc
	STC
	RET
.PAGE
	.SBTTL	donebyt - One byte instruction without operand

;Here for one byte instruction without operand

donebyt:MOVB	AL, #1			;This is a 1 byte item
	JMP	opsout.S

	.SBTTL	dtwobyt - Two byte instruction without operand

;Here for two byte instruction without operand

dtwobyt:MOVB	AL, #2			;This is a two byte item
	JMP	opsout.S

;Here for three byte instruction without operand

dthrbyt:MOVB	AL, #3			;This is a three byte item
opsout:	ADDB	opnsiz, AL
.IF EQ $$REALMODE
	MOVZWL	EBX, CS:opo_name32.B[ESI] ;Get address of string (assume 32 bit)
.IFF
	MOVW	BX, CS:opo_name32.B[SI]
.ENDC
	TESTB	oprsz16, #1		;Right?
	JE	2$.S			;Yes
.IF EQ $$REALMODE
	MOVZWL	EBX, CS:opo_name16.B[ESI] ;No - its 16 bit
.IFF
	MOVW	BX, CS:opo_name16.B[SI]
.ENDC
2$:	CALL	oopstr			;Output the string
	JMP	outtab			;Display tab and return

	.SBTTL	doneopr - Instruction with single operand

;Here for instruction with single operand

doneopr:MOVB	AL, #2			;Assume this is a 2 byte instruction
	CALL	opsout			;Output opcode
	MOVL	EAX, opxofs		;Store address of possible offset
	ADDL	EAX, #2.B
	MOVL	opofset, EAX
	CALL	getoo1			;Get mode byte
	JC	hvaerr.S
.PAGE
;Subroutine to display operand
;	CALL	doprnd
;	C:clr = Normal
;	C:set = Memory error, message already displayed

doprnd:	MOVB	AL, value+1		;Get mode byte
	ANDB	AL, #0C7h		;Remove opcode extension or register
					;  bits
	TESTB	adrsz16, #1		;Is this a 16 bit address mode?
	JE	10$.S			;No - go handle 32 bit case
	CMPB	AL, #6			;Yes - direct memory address?
	JE	d1opmem			;Yes - go handle that case
	MOVZBL	EBX, AL			;Dispatch on the mod bits
	SHRL	EBX, #4
	JMPIL	CS:d1opdsp16[EBX]

;Dispatch table for mod bits for operand

	.MOD	4
d1opdsp16:
	.LONG	d1op0d16		;00xxxxxx - No displacement value
	.LONG	d1op8d			;01xxxxxx - 8 bit displacement value
	.LONG	d1op16d16		;10xxxxxx - 16 bit displacement value
	.LONG	d1opreg			;11xxxxxx - Register

;Here for 32 bit address modes

10$:	CMPB	AL, #5			;Yes - direct memory address?
	JE	d1opmem.S		;Yes - go handle that case
	MOVZBL	EBX, AL			;Dispatch on the mod bits
	CMPB	AL, #04h		;Is there an sib byte?
	JE	12$.S
	CMPB	AL, #44h
	JE	12$.S
	CMPB	AL, #84h
	JNE	18$.S			;No
12$:	PUSHL	EBX			;Yes - get it
	MOVL	EBX, opxofs		;Get sib byte
	INCL	EBX
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmbyte
	MOVB	value+2, AL
	INCB	opnsiz			;Adjust item size
	INCL	opofset			;Adjust address of offset value
	POPL	EBX
	ANDB	AL, #7
	CMPB	AL, #5			;Is this the d32+(scaled index) mode?
	JNE	18$.S			;No
	TESTB	BL, #0C0h		;Maybe
	JE	d1op32d32		;Yes
18$:	SHRL	EBX, #4			;Dispatch on the mod bits
	JMPIL	CS:d1opdsp32[EBX]

;Dispatch table for mod bits for operand

	.MOD	4
d1opdsp32:
	.LONG	d1op0d32		;00xxxxxx - No displacement value
	.LONG	d1op8d			;01xxxxxx - 8 bit displacement value
	.LONG	d1op32d32		;10xxxxxx - 32 bit displacement value
	.LONG	d1opreg			;11xxxxxx - Register
.PAGE
;Here for 16 bit memory address operand

d1opmem:MOVZBL	EAX, segpfx		;Remember segment prefix
	PUSHL	EAX
	CALL	dsegspc			;Display segment specification
	MOVL	EBX, opofset		;Get address of memory offset
	MOVL	EDX, opnseg
	TESTB	adrsz16, #1		;16 bit address?
	JE	2$.S			;No
	ADDB	opnsiz, #2		;Yes - adjust item size
	ADDL	opofset, #2.B
	CALL	getmword		;Get word from memory
	JMP	4$.S

2$:	ADDB	opnsiz, #4		;Adjust item size
	ADDL	opofset, #4.B
	CALL	getmlong		;Get long from memory
4$:	JC	10$.S			;If error
	MOVL	EDI, EBX
	SUBL	EDI, opxofs
	MOVL	value[EDI], EAX
	MOVL	EDX, uregDS
	POPL	EBX			;Get segment prefix value
	TESTL	EBX, EBX		;Did we have one?
	JE	6$.S			;No
	MOVL	EDX, uregES-2[EBX*2]	;Yes - get right segment prefix value
6$:	CALL	outaddr			;Display value of the word as
	CLC				;  address in the data segment
	RET

10$:	POPL	EAX
	JMP	hvaerr
.PAGE
;Here for operand with 8 bit displacement value (16 or 32 bit modes)
;  This is displayed as disp.B[R1] or disp.B[R1+R2] (for 16 bit modes) or
;  as disp.B[R1], disp.B[R1+R2], disp.B[R2*n], or disp.B[R1+R2*n] (32 bit
;  modes)

d1op8d:
	CALL	dsegspc			;Display segment specification
	INCB	opnsiz			;Adjust item size
	MOVL	EBX, opofset		;Get 8 bit offset
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	INCL	opofset
	MOVL	EDI, EBX
	SUBL	EDI, opxofs
	MOVB	value[EDI], AL
	MOVXBL	EAX, AL
	CALL	outby0			;Display value of the byte
	MOVB	AL, #'.'
	CALL	outchr
	MOVB	AL, #'B'
	CALL	outchr
	TESTB	adrsz16, #1		;16 bit address mode?
	JNE	d1op0d16.S		;Yes - display 16 bit register part
	JMP	d1op0d32		;No - display 32 bit register part

;Here for operand with 16 bit displacement value (16 bit mode)
;  This is displayed as disp[R1] or disp[R1+R2]

d1op16d16:
	CALL	dsegspc			;Display segment specification
	ADDB	opnsiz, #2		;Adjust item size
	MOVL	EBX, opofset		;Get 16 bit offset
	MOVL	EDX, opnseg
	CALL	getmword
	JC	hvaerr
	ADDL	opofset, #2.B
	MOVL	EDI, EBX
	SUBL	EDI, opxofs
	CWD
	MOVL	value[EDI], EAX
	CALL	outwr0			;Display value of word

;Here for operand with no displacement value (16 bit mode)
;  This is displayed as [R1] or [R1+R2]

d1op0d16:
	CALL	dsegspc			;Display segment specification
	MOVZBL	EBX, value+1		;Get the r/m bits
	ANDB	BL, #7
	LEAL	EBX, d1indx16[EBX*8]	;Get address of string
	JMP	outstrc			;And go output it
.PAGE
;Here for register operand (16 and 32 bit modes)

d1opreg:MOVZBL	EBX, rgopsz		;Get way we should display this
	JMPIW	CS:opregdsp[EBX]	;And go do it

opregdsp:
	.WORD	dspreg		;SZ_OPR = 0 - Display as size of operand
	.WORD	dspreg8		;SZ_8   = 2 - Display as 8 bit register
	.WORD	dspreg16	;SZ_16  = 4 - Display as 16 bit register
	.WORD	dspreg32	;SZ_32  = 6 - Display as 32 bit register

;Subroutine to display opeand reigster (8, 16, or 32 bit)
;	c(AL) = Register bits

dspreg:	TESTB	value, #1		;Is this a byte register?
	JE	dspreg8.S		;Yes
dspregwl:
	TESTB	oprsz16, #1		;16 bit operand?
	JNE	dspreg16.S		;Yes
dspreg32:
	ANDL	EAX, #7			;Only 3 bits
	LEAL	EBX, r16bit[EAX*2]
	MOVB	AL, #'E'		;No - 32 bit - start with E
	CALL	outchr
	JMP	8$.S

;Here if have 8 bit register

dspreg8:ANDL	EAX, #7			;Only 3 bits
	LEAL	EBX, r8bit[EAX*2]
	JMP	8$.S

;Here if have 16 bit register

dspreg16:
	ANDL	EAX, #7
	LEAL	EBX, r16bit[EAX*2]
	JMP	8$.S

;Subroutine to display segment register operand
;	c(AL) = Register bits
;	CALL	dspseg

dspseg:	ANDL	EAX, #7
	LEAL	EBX, rsegmt[EAX*2]
8$:	MOVB	AL, CS:[EBX]		;Get first character
	CALL	outchr			;Output it
	MOVB	AL, CS:1.B[EBX]		;Get second character
	JMP	outchr			;Output it and return
.PAGE
;Here for operand with 32 bit displacement value (32 bit mode)
;  This is displayed as disp[R1], disp[R1+R2], disp[R2*n], or disp[R1+R2*n]

d1op32d32:
	CALL	dsegspc			;Display segment specification
	ADDB	opnsiz, #4		;Adjust item size
	MOVL	EBX, opofset		;Get 32 bit offset
	MOVL	EDX, opnseg
	CALL	getmlong
	JC	hvaerr
	ADDL	opofset, #4.B
	MOVL	EDI, EBX
	SUBL	EDI, opxofs
	MOVL	value[EDI], EAX
	CALL	outln0			;Display value of long

;Here for operand with no displacement value (32 bit mode)
;  This is displayed as [R1], [R1+R2], [R2*n], or [R1+R2*n]

d1op0d32:
	CALL	dsegspc			;Display segment specification
	MOVZBL	EBX, value+1		;Get the r/m bits
	ANDB	BL, #7
	CMPB	BL, #4			;Is the sib byte present?
	JE	havesib.S		;Yes
	LEAL	EBX, d1indx32[EBX*8]	;No - get address of string
	JMP	outstrc			;And go output it

;Here if have an sib byte

havesib:MOVB	AL, #'['		;Start out with left bracket
	CALL	outchr
	MOVW	AX, value+1		;Get rm and sib bytes
	ANDW	AX, #07C0h		;Have base register?
	CMPW	AX, #0500h
	JE	6$.S			;No
	MOVB	AL, AH			;Yes - display it
	CALL	dspreg32
	MOVB	AL, value+2		;Do we have an index register?
	ANDB	AL, #38h
	CMPB	AL, #20h
	JE	10$.S			;No
	MOVB	AL, #'+'		;Yes - output plus
	CALL	outchr
6$:	MOVB	AL, value+2		;Get index value
	SHRB	AL, #3
	ANDB	AL, #7
	CMPB	AL, #4			;Do we have one?
	JE	10$.S			;No
	CALL	dspreg32		;Yes - display it
10$:	MOVZBL	EAX, value+2		;Get scale factor value times 4
	SHRB	AL, #4
	ANDB	AL, #0Ch
	LEAL	EBX, scltbl[EAX]
	JMP	outstrc
.PAGE
;Table which gives 16 bit index register string as function of the r/m bits

d1indx16:
	.ASCIZ	"[BX+SI]"	;000
	.ASCIZ	"[BX+DI]"	;001
	.ASCIZ	"[BP+SI]"	;010
	.ASCIZ	"[BP+DI]"	;011
	.ASCIZ	"[SI]"{0,0,0}	;100
	.ASCIZ	"[DI]"{0,0,0}	;101
	.ASCIZ	"[BP]"{0,0,0}	;110
	.ASCIZ	"[BX]"{0,0,0}	;111

;Table which gives 32 bit index register string as function of the r/m bits

d1indx32:
	.ASCIZ	"[EAX]"{0,0}	;000
	.ASCIZ	"[ECX]"{0,0}	;001
	.ASCIZ	"[EDX]"{0,0}	;010
	.ASCIZ	"[EBX]"{0,0}	;011
	.ASCIZ	"[???]"{0,0}	;100
	.ASCIZ	"[EBP]"{0,0}	;101
	.ASCIZ	"[ESI]"{0,0}	;110
	.ASCIZ	"[EDI]"{0,0}	;111

;Table which gives 32 bit index register scale value as function of the
;  index field value

scltbl:	.ASCIZ	"]"{0,0}	;00
	.ASCIZ	"*2]"		;01
	.ASCIZ	"*4]"		;10
	.ASCIZ	"*8]"		;11

;Table which gives register names as function of the reg bits in the mode word

r16bit:	.ASCII	"AX"		;000 - 16 bit registers
	.ASCII	"CX"		;001
	.ASCII	"DX"		;010
	.ASCII	"BX"		;011
	.ASCII	"SP"		;100
	.ASCII	"BP"		;101
	.ASCII	"SI"		;110
	.ASCII	"DI"		;111

r8bit:	.ASCII	"AL"		;000 - 8 bit registers
	.ASCII	"CL"		;001
	.ASCII	"DL"		;010
	.ASCII	"BL"		;011
	.ASCII	"AH"		;100
	.ASCII	"CH"		;101
	.ASCII	"DH"		;110
	.ASCII	"BH"		;111

rsegmt:	.ASCII	"ES"		;000 - Segment registers
	.ASCII	"CS"		;001
	.ASCII	"SS"		;010
	.ASCII	"DS"		;011
	.ASCII	"FS"		;100
	.ASCII	"GS"		;101
	.BYTE	0, 0		;110
	.BYTE	0, 0		;111
.PAGE
	.SBTTL	dregopr - Instruction with single general register operand

;Here for instruction with single general register operand

dregopr:MOVB	AL, #1			;This is a 1 byte instruction
	CALL	opsout			;Display opcode
dregop2:MOVB	AL, value		;Get register bits from opcode byte
	JMP	dspregwl		;Go display register

	.SBTTL	dmemxseg - MOV instruction with segment register operand

;Here for MOV instruction with segment register operand

dmemxseg:
	XORB	value, #1		;Make the w bit look right
	TESTB	value, #2		;Going to segment register?
	JE	dmemxs2.S		;No
	MOVB	segchgd, #1		;Yes - remember that
dmemxs2:MOVL	EAX, #dspseg		;Get address of routine to display
					;  segment register
	JMP	dtwoop2			;Call common code

	.SBTTL	dmovcr, dmovdr, dmovtr - MOV instruction with control register operand

;Here for MOV instruction with control register (CRn) operand

dmovcr:	PUSHL	#'C'.B
	JMP	dmovxr.S

;Here for MOV instruction with control register (DRn) operand

dmovdr:	PUSHL	#'D'.B
	JMP	dmovxr.S

;Here for MOV instruction with control register (TRn) operand

dmovtr:	PUSHL	#'T'.B
dmovxr:	MOVB	AL, #3			;This is a 3 byte instruction
	CALL	opsout			;Output opcode
	TESTB	value+1, #2		;To control register?
	JNE	10$.S			;Yes
	MOVB	AL, value+2		;No - display general register
	CALL	dspreg32
	CALL	outcma
	POPL	EAX			;Get first letter for control register
dspcrg:	CALL	outchr			;  name
	MOVB	AL, #'R'		;Display control register name
	CALL	outchr
	MOVB	AL, value+2
	SHRB	AL, #3
	ANDB	AL, #7
	ADDB	AL, #'0'
	JMP	outchr

;Here if moving to control register

10$:	POPL	EAX			;Get first letter
	CALL	dspcrg			;Display control register
	CALL	outcma			;Followed by comma
	MOVB	AL, value+2		;Followed by general register
	JMP	dspreg32
.PAGE
	.SBTTL	dtwoopm16 - Instruction with 16 bit mem dst and 16 bit reg src

;Here for instruction with 16 bit memory destination and 16 bit memory source

dtwoopm16:
	MOVB	value, #1		;Fix up the d and w bits
	MOVB	oprsz16, #1		;Indicate 16 bit operands
	JMP	dtwoopr.S		;Continue with common code

	.SBTTL	dtwoopx - Instruction with two 16 or 32 bit operands (to register)

;Here for instruction with two 16 or 32 bit operands with register destination

dtwoopx:ORB	value, #3		;Fix up the d and w bits

	.SBTTL	dtwoopr - Instruction with two operands

;Here for instruction with two opeands

dtwoopr:MOVL	EAX, #dspreg		;Get address of routine to display
dtwoop2:PUSHL	EAX			;  register
	MOVB	AL, #2			;Assume size is 2
	CALL	opsout
	CALL	getoo1			;Get mode byte
	JC	hvaerr
	MOVL	EAX, opxofs		;Store address of possible offset
	ADDL	EAX, #2.B
	MOVL	opofset, EAX
	TESTB	value, #2		;Which way is this going?
	JNE	dtwo2rg			;To register
	CALL	doprnd			;To memory - display memory operand
	JC	hvaerr
	CALL	outcma			;Followed by a comma
	POPL	EDI			;Get address of register display routine
dtwoo2:	MOVB	AL, value+1		;Get reg bits
	SHRB	AL, #3
	JMPIL	EDI			;Followed by register operand

;Here if register is destination

dtwo2rg:POPL	EDI			;Get address of register display routine
	CALL	dtwoo2			;Display register operand
	JC	hvaerr
	CALL	outcma			;Followed by a comma
	JMP	doprnd			;Followed by memory operand
.PAGE
	.SBTTL	doneflt - One operand floating point instructions

;Here for one operand floating point instructions whose operand is a floating
;  point register.  Note that there is also a no register form for some of
;  these, but that's handled by a separate table entry

doneflt:CALL	dtwobyt			;Display opcode
	MOVB	AL, value+1		;Get register number
	AND%	%AX, #7h.B
	JMP	6$.S			;Continue

	.SBTTL	dtwoflt - Two operand floating point instructions

;Here for two operand floating point instructions whose operands are floating
;  point registers.  These may be displayed with 1 or 2 operands as follows:
;	Fxxx  STn, ST0  ==>  Fxxx  STn
;	Fxxx  ST0, STn  ==>  Fxxx  ST0, STn
;  Note that there is also a no register form for some of these, but that's
;  handled by a separate table entry

dtwoflt:CALL	dtwobyt			;Display opcode
	MOVB	AL, value+1		;Get register number
	AND%	%AX, #7h.B
	TESTB	value+0, #04h
	JNE	2$.S
	XCHGB	AL, AH
2$:	CMPB	AH, #0
	JE	6$.S
	PUSH%	%AX
	CALL	outfprg
	CALL	outcma
	POP%	%AX
	MOVB	AL, AH
6$:	CALL	outfprg
	JMP	outtab

outfprg:PUSH%	%AX
	MOVB	AL, #'S'
	CALL	outchr
	MOVB	AL, #'T'
	CALL	outchr
	POP%	%AX
	ADDB	AL, #'0'
	JMP	outchr
.PAGE
	.SBTTL	dimul - IMUL instruction

;Here for the IMUL instruction

dimul:	MOVB	AL, #3			;This is basically a 3 byte instruction
	CALL	opsout
	MOVL	EAX, opxofs
	ADDL	EAX, #2.B
	MOVL	opofset, EAX
	CALL	getoo1			;Get mode byte
	JC	hvaerr
	MOVB	CH, AL
	MOVB	AH, AL
	MOVB	CL, #3
	SHRB	AL, CL
	ANDB	AH, #0C0h		;See if first two operands are the
	CMPB	AH, #0C0h		;  same
	JNE	4$			;No
	XORB	CH, AL
	ANDB	CH, #7
	JE	di2m03.S		;Yes
4$:	CALL	dspreg			;No - display register
	JC	hvaerr
	CALL	outcma			;Followed by a comma
	JMP	di2m03.S		;Continue with common stuff
.PAGE
	.SBTTL	dim2mem - Immediate operand to memory without size bit

;Here for immediate operand to memory without size bit

dim2mem:ANDB	value, #1		;Only care about w bit here

	.SBTTL	dims2mem - Immediate operand to memory with size bit

;Here for immediate operand to memory with size bit

dims2mem:
	MOVB	AL, #3			;This is at least a 3 byte instruction
	CALL	opsout
	MOVL	EAX, opxofs
	INCL	EAX
	INCL	EAX
	MOVL	opofset, EAX
	CALL	getoo1			;Get mode byte
	JC	hvaerr
di2m03:	CALL	doprnd			;Display destination operand
	JC	hvaerr
	CALL	outcma			;Followed by a comma
	MOVB	AL, #'#'		;Followed by #
	CALL	outchr
	MOVL	EBX, opofset
	MOVL	EDX, opnseg
	TESTB	value, #1		;8 bit instruction?
	JNE	di2m06.S		;No
	CALL	getmbyte		;Yes - get 8 bit immediate value
	JC	hvaerr
	JMP	outbyte

;Here if word or long instruction

di2m06:	TESTB	value, #2		;Have 8 bit value?
	JE	6$.S			;No
	CALL	getmbyte		;Yes - get it
	JC	hvaerr
	CALL	outbyte			;Display it followed by .B
	MOVL	EBX, #dotbstr
	JMP	outstrc

;Here if have full length immediate value

6$:	TESTB	oprsz16, #1		;16 bit instruction?
	JE	8$.S			;No - 32 bit
	CALL	getmword		;Yes - get 16 bit immediate value
	JC	hvaerr
	INCB	opnsiz			;Bump length of instruction
	JMP	outword

8$:	CALL	getmlong		;Get 32 bit immediate value
	JC	hvaerr
	ADDB	opnsiz, #3
	JMP	outlong
.PAGE
	.SBTTL	dim2reg - Immediate operand to general register

;Here for immediate operand to general register

dim2reg:MOVB	AL, #2			;This is a 2 or 3 byte instruction
	CALL	opsout
	MOVB	AL, value		;Get register bits
	RORB	value, #3		;Get the w bit in the normal place
	CALL	dspreg			;Display register
di2r02:	CALL	outcma
	MOVB	AL, #'#'
	CALL	outchr
	MOVL	EBX, opxofs
	INCL	EBX
	MOVL	EDX, opnseg
	TESTB	value, #1		;Word operation?
	JNE	4$.S			;Yes
	CALL	getmbyte		;No - get immediate byte
	JC	hvaerr
	JMP	outbyte			;Display the byte

4$:	INCB	opnsiz			;Fix up the size
	TESTB	oprsz16, #1		;16 bit operand?
	JE	6$.S			;No
	CALL	getmword		;Yes - get immediate word
	JC	hvaerr
	JMP	outword

6$:	ADDB	opnsiz, #2
	CALL	getmlong
	JC	hvaerr
	JMP	outlong

	.SBTTL	dim2ax - Immediate operand to AX or AL

;Here for immediate operand to AX or AL

dim2ax:	MOVB	AL, #2			;This is a 2 or 3 byte instruction
	CALL	opsout			;Display opcode
	CALL	daxoal			;Display AX or AL
	JMP	di2r02.S		;Continue with common code
.PAGE
	.SBTTL	dmemxax - MOV instruction with AX or AL as one operand

;Here for MOV instruction with AX or AL as one operand

dmemxax:MOVB	AL, #3			;This is at least a 3 byte instruction
	CALL	opsout
	TESTB	value, #2		;Which way?
	JNE	dax2mem.S		;To memory
	CALL	daxoal			;To register - display AX or AL
	CALL	outcma			;And a comma
dmxax02:MOVZBL	EBX, segpfx		;Save segment prefix value
	PUSHL	EBX
	CALL	dsegspc			;Display segment override if needed
	MOVL	EBX, opxofs		;Get memory address
	INCL	EBX
	MOVL	EDX, opnseg
	TESTB	adrsz16, #1		;16 bit operand?
	JNE	2$.S			;Yes
	ADDB	opnsiz, #2		;No - 32 bit
	CALL	getmlong
	POPL	EBX
	JC	hvaerr
	PUSHL	#'L'.B
	JMP	4$.S

2$:	CALL	getmword
	POPL	EBX
	JC	hvaerr
	PUSHL	#'W'.B
4$:	MOVL	EDX, uregDS
	TESTL	EBX, EBX		;Did we have a segment specified?
	JE	6$.S			;No
	MOVL	EDX, uregES-2[EBX*2]	;Yes - get right segment prefix value
6$:	CALL	outaddr			;Display address
	JMP	ddirj4

;Here if moving from register to memory

dax2mem:CALL	dmxax02			;Display memory adderss
	JC	hvaerr
	CALL	outcma			;Followed by a comma
					;Fall into daxoal to display AX or AL

	.SBTTL	daxoal - Subroutine to display AX or AL

;Subroutine to display AX or AL
;	CALL	daxoal

daxoal:	TESTB	oprsz16, #1		;16 bit operand?
	JNE	14$.S			;Yes
	TESTB	value, #1		;Byte operation?
	JE	14$.S			;Yes
	MOVB	AL, #'E'		;No - 32 bit - start out with E
	CALL	outchr
14$:	MOVB	AL, #'A'		;Always start out with A
	CALL	outchr
	MOVB	AL, #'X'		;Assume want X next
	TESTB	value, #1		;Really a word operation?
	JNE	di2ax02.S		;Yes
	MOVB	AL, #'L'		;No - display L next
di2ax02:JMP	outchr
.PAGE
	.SBTTL	dpushim - PUSH instruction with immediate operand

;Here for PUSH instruction with immediate operand

dpushim:MOVB	AL, #2			;Assume 2 bytes
	CALL	opsout
	MOVB	AL, #'#'
	CALL	outchr
	MOVL	EBX, opxofs		;Get address of operand
	INCL	EBX
	MOVL	EDX, opnseg
	JMP	di2m06
.PAGE
	.SBTTL	dsegreg - One byte instructions with single segment register operand

;Here for one byte instructions with single segment register operand

dsegrgc:MOVB	segchgd, #1		;Indicate segment register changed
dsegreg:CALL	donebyt			;Display opcode
	MOVB	AL, value		;Get register bits
	JMP	10$.S			;Continue

	.SBTTL	dsg3reg - Two byte instructions with single segment register operand

;Here for two byte instructions with single segment register operand

dsg3rgc:MOVB	segchgd, #1		;Indicate segment register changed
dsg3reg:CALL	donebyt			;Display opcode
	INCB	opnsiz			;Fix up the size
	MOVB	AL, value+1		;Get register bits
10$:	SHRB	AL, #3
	JMP	dspseg			;Go display segment register
.PAGE
	.SBTTL	dsrotone - Single bit shift and rotate instructions

;Here for single bit shift and rotate instructions

dsrotone:
	CALL	doneopr			;Display first operand
	JC	hvaerr
	MOVL	EBX, #cl1str		;Followed by ,#1
	JMP	dsrot02.S

	.SBTTL	dsrotcl - Variable shift and rotate instructions

;Here for variable shift and rotate instructions

dsrotcl:CALL	doneopr			;Display first operand
	JC	hvaerr
	MOVL	EBX, #cclstr		;Followed by ,CL
dsrot02:JMP	outstrc

	.SBTTL	dsrotim - Immediate shift and rotate instructions

;Here for immediate shift and rotate instructions

dsrotim:CALL	doneopr			;Display first operand
	JC	hvaerr
	CALL	outcma
	INCB	opnsiz
	MOVB	AL, #'#'
	CALL	outchr
	MOVL	EBX, opofset
	MOVL	EDX, opnseg
	CALL	getmbyte		;Get 8 bit immediate value
	JC	hvaerr
	JMP	outby0			;And display the byte value
.PAGE
	.SBTTL	dxchgax - XCHG instruction when one operand is AX

;Here for XCHG instruction when one operand is AX or AL

dxchgax:CALL	donebyt			;Display opcode
	JC	hvaerr
	MOVL	EBX, #axcstr		;Followed by AX,
	CALL	outstrc
	JMP	dregop2			;Followed by register operand

	.SBTTL	dentrins - ENTER instruction

;Here for ENTER instruction

dentrins:
	MOVB	AL, #4
	CALL	opsout			;Display opcode
	MOVL	EBX, opxofs		;Get first operand value
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmword
	JC	hvaerr
	CALL	outwr0			;Display it
	CALL	outcma			;Followed by a comma
	MOVL	EBX, opxofs		;Get second operand value
	ADDL	EBX, #3.B
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	JMP	outby0			;Display it
.PAGE
	.SBTTL	dloop - Conditional looping instructions

;Here for the conditional looping instructions

dloop:	MOVB	AL, #2
	CALL	opsout			;Display opcode
	TESTB	adrsz16, #1		;16 bit operation?
	JNE	2$.S			;Yes
	MOVB	AL, #'E'		;No
	CALL	outchr
2$:	MOVL	EBX, #cxcstr		;Display rest of register name
	CALL	outstrc
	JMP	4$.S

	.SBTTL	dbranch - Conditional and short jump instructions

;Here for conditional and short jump instructions

dbranch:MOVB	AL, #2
	CALL	opsout			;Display opcode
4$:	MOVL	EBX, opxofs		;Get offset value
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	CBW
	CWL
	ADDL	EAX, #2.B
	MOVB	CL, #'S'
	JMP	ddirj2

	.SBTTL	dlngbr - Long conditional jump instructions

;Here for long conditional jump instructions

dlngbr:	MOVB	AL, #4			;This is a 4 (or 6) byte instruction
	CALL	opsout			;Display opcode
	MOVL	EBX, opxofs		;Get offset value
	INCL	EBX
	INCL	EBX
	MOVL	EDX, opnseg
	TESTB	oprsz16, #1
	JE	8$.S
	CALL	getmword
	JC	hvaerr
	CWL
	ADDL	EAX, #4.B
	MOVB	CL, #'R'
	JMP	ddirj2.S

8$:	CALL	getmlong
	JC	hvaerr
	ADDL	EAX, #6.B
	ADDB	opnsiz, #2
	MOVB	CL, #'Q'
	JMP	ddirj2.S
.PAGE
	.SBTTL	ddirjmpn - Direct (16 bit) near jump and call instructions

;Here for direct (16 bit offset) near jump and call instructions

ddirjmpn:
	MOVB	AL, #3			;This is a 3 (or 5) byte instruction
	CALL	opsout
	MOVL	EBX, opxofs		;Get address of diplacement value
	INCL	EBX
	MOVL	EDX, opnseg
	TESTB	oprsz16, #1
	JE	4$.S
	CALL	getmword
	JC	hvaerr
	CWL
	ADDL	EAX, #3.B
	MOVB	CL, #'R'
	JMP	ddirj2.S

4$:	CALL	getmlong
	JC	hvaerr
	ADDL	EAX, #5.B
	ADDB	opnsiz, #2
	MOVB	CL, #'Q'
ddirj2:	PUSHL	ECX
	ADDL	EAX, opxofs
	MOVL	EDX, opnseg
	CALL	outaddr			;Display address
ddirj4:	MOVB	AL, #'.'
	CALL	outchr
	POPL	EAX
	CALL	outchr
	CLC
	RET
.PAGE
	.SBTTL	ddirjmpf - Direct far jump and call instructions

;Here for direct far jump and call instructions

ddirjmpf:
	MOVB	AL, #5			;This is a 5 (or 7) byte instruction
	CALL	opsout
	MOVL	EBX, opxofs		;Get address of memory offset
	INCL	EBX
	MOVL	EDX, opnseg
	TESTB	oprsz16, #1
	JE	14$.S
	CALL	getmword
	JC	hvaerr
	CWL
	INCL	EBX
	INCL	EBX
	MOVB	CL, #'W'
	JMP	16$.S

14$:	CALL	getmlong
	JC	hvaerr
	ADDL	EBX, #4.B
	ADDB	opnsiz, #2
	MOVB	CL, #'L'
16$:	PUSHL	EAX
	CALL	getmword		;Get segment selector
	POPL	EDX
	JC	hvaerr
	PUSHL	ECX
	XORL	EAX, opnseg		;Make it the same type selector as
	XORW	AX, opnseg		;  the address we are looking at
	XCHGL	EAX, EDX
	CALL	outsgof
	JMP	ddirj4.S
.PAGE
	.SBTTL	dretfix - Return instruction with stack fixup

;Here for return instruction with stack fixup

dretfix:MOVB	AL, #3			;This is a 3 byte instruction
	CALL	opsout
	MOVB	AL, #'#'
	CALL	outchr
	MOVL	EBX, opxofs		;Get immediate value
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmword
	JC	hvaerr
	MOVW	value+1, AX
	CWD
	CALL	outwr0			;Display the value	
	CLC
	RET

	.SBTTL	dintins - INT instruction

;Here for INT instruction

dintins:MOVB	AL, #2			;This is a 2 byte instruction
	CALL	opsout
	MOVL	EBX, opxofs		;Get second byte
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	CALL	outby0			;Display the value	
	CLC
	RET
.PAGE
	.SBTTL	diofix - IO instruction with fixed IO port

;Here for IO instruction with fixed IO port

diofix:	MOVB	AL, #2			;This is a 2 byte instruction
	CALL	opsout
	MOVL	EBX, opxofs		;Get second byte
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	CALL	outby0			;Display the value	
	CLC
	RET

	.SBTTL	diovar - IO instruction with variable IO port

;Here for IO instruction with variable IO port

diovar:	CALL	donebyt			;Display opcode
	MOVL	EBX, #xdxstr		;Followed by [DX]
	CALL	outstrc
	CLC
	RET
.PAGE
	.SBTTL	dlngimdb - Long format immediate mode instruction with byte value

dlngimdb:
	MOVB	value, #3		;Make this look like both s and w are
	MOVW	AX, value+2		;  set
	MOVW	value+1, AX
	INCL	opxofs
	MOVB	AL, #4			;This is a 4 byte instruction
	CALL	opsout
	MOVL	EAX, opxofs
	ADDL	EAX, #2.B
	MOVL	opofset, EAX
	CALL	doprnd			;Display destination operand
	JC	hvaerr
	CALL	outcma			;Followed by a comma
	MOVB	AL, #'#'		;Followed by #
	CALL	outchr
	MOVL	EBX, opofset
	MOVL	EDX, opnseg
	CALL	getmbyte		;Get 8 bit immediate value
	JC	hvaerr
	JMP	outbyte
.PAGE
	.SBTTL	dmovxb - Move byte extended instructions

dmovxb:	MOVB	rgopsz, #SZ_8
	JMP	2$.S

	.SBTTL	dmovxw - Move word extended instructions

dmovxw:	MOVB	rgopsz, #SZ_16
2$:	MOVB	AL, value+1
	MOVB	value, AL
	INCL	opxofs
	INCB	opnsiz
	MOVL	EAX, #dspregwl
	JMP	dtwoop2			;Display instruction

	.SBTTL	dlng1bopr - Long format instructions with one byte operand

;Here for long format instructions with one byte operand

dlng1bopr:
	MOVB	value, #0		;Clear "w bit"
	JMP	4$.S

	.SBTTL	dlng1wopr - Long format instructions with one word operand

;Here for long format instructions with one word operand

dlng1wopr:
	MOVB	oprsz16, #1		;Force operand size to 16 bits

	.SBTTL	dlng1wlopr - Long format instructions with one word or long operand

;Here for long format instructions with one word or long operand

dlng1wlopr:
	MOVB	value, #1		;Set "w bit"
4$:	INCL	opxofs
	INCB	opnsiz
	JMP	doneopr

	.SBTTL	dlngsdim - Long format shift double with immediate count

dlngsdim:
	CALL	dlng2oprs		;Display all but last field
	INCB	opnsiz			;Include immediate byte in the size
	CALL	outcma			;Followed by a comma
	MOVB	AL, #'#'		;Followed by #
	CALL	outchr
	MOVL	EBX, opofset
	MOVL	EDX, opnseg
	CALL	getmbyte		;Get 8 bit immediate value
	JC	hvaerr
	JMP	outbyte			;And display it

	.SBTTL	dlngsdcl - Long format shift double with variable count

dlngsdcl:
	CALL	dlng2oprs		;Display all but last field
	MOVL	EBX, #cclstr		;Last field is always CL
	JMP	outstrc

	.SBTTL	dlng2oprs - Long format instructions with two operands and register source

;Here for long format instructions with two operands and register source

dlng2oprs:
	MOVB	value, #1
	JMP	14$.S

	.SBTTL	dlng2oprd - Long format instructions with two operands and register destination

;Here for long format instructions with two operands and register destination

dlng2oprd:
	MOVB	value, #3
	JMP	14$.S

	.SBTTL	dlng2opr - Long format instructions with two operands

;Here for long format instructions with two operands

dlng2opr:
	MOVB	AL, value+1
	MOVB	value, AL
14$:	INCL	opxofs			;Adjust offset for rest of instruction
					;  bytes
	INCB	opnsiz
	JMP	dtwoopr			;Display instruction
.PAGE
	.SBTTL	dstring, dstrcon - String instructions

;Here for the unconditional string instructions

dstring:CMPB	reppfx, #0		;Have repeat prefix?
	JE	6$.S			;No
	MOVB	AL, #'R'		;Yes - prefix it with an R
	CALL	outchr
	JMP	6$.S

;Here for the conditional string instructions

dstrcon:CMPB	reppfx, #0		;Have repeat prefix?
	JE	6$.S			;No
	MOVB	AL, #'R'		;Yes - start out with R
	CALL	outchr
	MOVB	AL, #'N'		;Assume test is for not equal
	TESTB	reppfx, #1		;Right?
	JE	4$.S			;Yes
	MOVB	AL, #'E'		;No - its for equal
4$:	CALL	outchr
6$:	CALL	donebyt			;Output opcode
	MOVB	AL, value		;Do we need a destination operand?
	ANDB	AL, #0FEh
	CMPB	AL, #0ACh
	JE	10$.S
	CMPB	AL, #6Eh
	JE	10$.S
	MOVL	EBX, #sdststr		;Yes - assume 16 bit address size
	TESTB	adrsz16, #1		;Right?
	JNE	8$.S			;Yes
	ADDL	EBX, #sedststr-sdststr.B ;No - use right string
8$:	CALL	outstrc
	TESTB	value, #08h		;Need source too?
	JNE	20$.S			;No - all done here
	CALL	outcma			;Yes - need comma next
10$:	CALL	dsegspc			;Output segment specification if needed
	MOVL	EBX, #ssrcstr		;Assume 16 bit address size
	TESTB	adrsz16, #1		;Right?
	JNE	12$.S			;Yes
	ADDL	EBX, #sesrcstr-ssrcstr.B ;No - use right string
12$:	JMP	outstrc

20$:	RET
.PAGE
	.SBTTL	dfltjmp - FLTJMP "instruction"

;Here for the FLTJMP "instruction" - this is not really an instruction but is
;  a sequence used by X-OS/386 to indicate an address to transfer to if the
;  preceeding instruction causes a memory exception - it is the sequence:
;	JMP	$+7.B
;	NOP
;	.LONG	address

dfltjmp:MOVB	AL, #7			;This is a 7 byte instruction
	CALL	opsout
	MOVL	EBX, opxofs		;Get address of memory offset
	ADDL	EBX, #3.B
	MOVL	EDX, opnseg
	CALL	getmlong		;Get address
	JC	hvaerr
	MOVL	EDX, opnseg
	CALL	outsgof
	CLC
	RET
.PAGE
	.SBTTL	dcrash - CRASH "instruction"

;Here for the CRASH "instruction" - this is really an INT 1F followed inlie by
;  four ASCII characters!

dcrash:	MOVB	AL, #6			;This is a 6 byte instruction
	CALL	opsout
	MOVL	EBX, opxofs		;Get address of ASCII characters
	INCL	EBX
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmlong		;Get characters
	JC	hvaerr			;If error
	MOVL	ECX, #4			;Output 4 characters
4$:	PUSHL	EAX
	CALL	outchr			;Output character
	POPL	EAX
	SHRL	EAX, #8			;Get next character
	LOOP	ECX, 4$			;Continue if more to output
	CLC				;Finished
	RET
.PAGE
	.SBTTL	dreppfx - Repeat prefix

;Here for the repeat prefix

dreppfx:MOVB	AL, value		;Store prefix value
	MOVB	reppfx, AL
	JMP	outopn.S		;Continue

	.SBTTL	doszpfx - Operand size prefix

;Here for the operand size prefix

doszpfx:XORB	oprsz16, #1		;Change the operand size indicator
	JMP	outopn.S

	.SBTTL	daszpfx - Address size prefix

;Here for the address size prefix

daszpfx:XORB	adrsz16, #1		;Change the operand size indicator
outopn:	INCL	opxofs			;Bump address
	MOVL	EBX, opxofs		;Get next byte
	MOVL	EDX, opnseg
	CALL	getmbyte
	JC	hvaerr
	MOVB	value, AL		;Save first byte
	INCB	opnsiz			;Adjust size
	JMP	outop2			;Display instruction

	.SBTTL	dsfspfx - Segment override prefix for FS

;Segment override prefix for FS

dsfspfx:MOVB	AL, #5
	JMP	10$.S

	.SBTTL	dsgspfx - Segment override prefix for GS

;Segment override prefix for GS

dsgspfx:MOVB	AL, #6
	JMP	10$.S

	.SBTTL	dsegpfx - Segment override prefix for ES, CS, SS, or DS

;Here for the segment override prefix for ES, CS, SS, or DS

dsegpfx:MOVB	AL, value		;Get prefix byte
	SHRB	AL, #3			;Get segment bits in right place
	ANDB	AL, #3
	INCB	AL
10$:	MOVB	segpfx, AL		;Remember the prefix
	JMP	outopn.S		;Display instruction prefix applies to
.PAGE
	.SBTTL	dsegspc - Subroutine to display segment specification

;Subroutine to display segment specificaiton
;	CALL	dsegspc

dsegspc:CMPB	segpfx, #0		;Do we need a segment specification?
	JE	ret410.S		;No
	MOVZBL	EBX, segpfx		;Yes - get segment
	MOVB	segpfx, #0
	LEAL	EBX, segstbl-4[EBX*4]	;Point to string
	JMP	outstrc			;Output it and return

;Segment specification table

segstbl:.ASCIZ	"ES:"
	.ASCIZ	"CS:"
	.ASCIZ	"SS:"
	.ASCIZ	"DS:"
	.ASCIZ	"FS:"
	.ASCIZ	"GS:"
.PAGE
	.SBTTL	getoo1 - Subroutine to get second byte of instruction

;Subroutine to get second byte of instruction
;	CALL	getoo1

getoo1:	MOVL	EBX, opxofs		;Get mode byte
	INCL	EBX
	MOVL	EDX, opnseg
	CALL	getmbyte
	MOVB	value+1, AL
ret410:	RET
.PAGE
;Subroutine to display opcode string
;	c{EBX} = Offset of opcode string (up to 6 characters, trailing space
;		   filled)
;	CALL	oopstr

oopstr:	MOV%	%CX, #7			;Output up to 7 characters
2$:	MOVB	AL, CS:[%BX]		;Get character
	CMPB	AL, #' '		;Space?
	JE	ret410.S		;Yes - done
	CALL	outchr			;No - output character
	INC%	%BX			;Bump pointer
	LOOP	%CX, 2$			;Loop back if more possible
	RET
.PAGE
bdomsg:	 .ASCIZ	{ERR}"?? "{TXT}
xdxstr:	 .ASCIZ	"[DX]"
cl1str:	 .ASCIZ	",#1"
cclstr:	 .ASCIZ	",CL"
axcstr:	 .ASCIZ	"AX,"
cxcstr:	 .ASCIZ	"CX,"
dotbstr: .ASCIZ	".B"
sdststr: .ASCIZ	"[DI]"
sedststr:.ASCIZ	"[EDI]"
ssrcstr: .ASCIZ	"[SI]"
sesrcstr:.ASCIZ	"[ESI]"

	DATA

	.MOD	4
opofset:.LONG	0
oofetch:.WORD	0
segpfx:	.BYTE	0		;Segment prefix flag
reppfx:	.BYTE	0		;Repeat prefix value
rgopsz:	.BYTE	0		;Size of register used as memory operand
adrsz16:.BYTE	0		;1 if 16 bit addresses, 0 if 32 bit
oprsz16:.BYTE	0		;1 if 16 bit operands, 0 if 32 bit

	CODE
.PAGE
