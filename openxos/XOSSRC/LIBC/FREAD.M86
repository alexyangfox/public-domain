	.TITLE	fread - Function to read block from IO stream
	.SBTTL	STDC run-time library for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

	.PSECT	_TEXT_p

	.SBTTL	fread - Function to read block from IO stream

;Function to read block from IO stream
;	const void *buffer;	/* Pointer to buffer */
;	int   size;		/* Size of each data item */
;	int   count;		/* Number of items to input */
;	FILE *stream;		/* Stream pointer */
;	(int)val = fread(buffer, size, count, stream);
;  Value returned is number of items input if normal or -1 if error (errno
;    is set)

$$$=!0
FRM fr_rtn, 4t
FRM fr_cnt, 4t
fr_SIZE=!$$$

	.ENTRY	fread
fread:	PUSHL	ESI
	PUSHL	EDI
	ENTER	fr_SIZE, 0
	MOVL	ESI, 16t+12t.B[EBP]	;Get stream pointer
	MOVL	EDI, 16t+0t.B[EBP]	;Get offset of buffer
	MOVL	EAX, 16t+8t.B[EBP]	;Get number of items to output
	MOVL	fr_rtn.B[EBP], EAX
	IMULL	EAX, 16t+4t.B[EBP]	;Times size of each item
	MOVL	fr_cnt.B[EBP], EAX
4$:	DECL	fr_cnt.B[EBP]		;More to do?
	JS	6$.S			;No - finished
	PUSHL	ESI			;Get byte
	CALL	fgetc#
	ADDL	ESP, #4.B
	TESTL	EAX, EAX
	JS	10$.S			;If error
	MOVB	[EDI], AL		;OK - store byte
	INCL	EDI
	JMP	4$.S

;Here when finished

6$:	MOVL	EAX, fr_rtn.B[EBP]
8$:	LEAVE
	POPL	EDI
	POPL	ESI
	RET

;Here if error

10$:	CMPL	errno#, #-ER_EOF.B	;EOF?
	JNE	8$.S
	MOVL	EDX, fr_rtn.B[EBP]	;Get total number of items minus 1
	IMULL	EDX, 16t+4t.B[EBP]
	DECL	EDX
	SUBL	EDX, fr_cnt.B[EBP]	;Did we get anything?
	JE	8$.S			;No - return EOF
	MOVL	EAX, EDX		;Yes
	CLRL	EDX
	IDIVL	16t+4t[EBP]		;Get number of whole elements read
	JMP	8$.S

	.END
