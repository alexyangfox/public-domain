	.TITLE	strrchr - ANSI C function to search string for rightmost chr.
	.SBTTL	STDC run-time library for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

;+
; Edit history:
;
;  Date    Who  Description
; -------  ---  -----------
;  1Sep94  FPJ  Optimized code.
;-

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

.MACRO	MOVSEG	dst, src, reg
.IF	NB	{reg}
	MOVL	reg, src
	MOVL	dst, reg
.IFF
	PUSHL	src
	POPL	dst
.ENDC
.ENDM

	.SBTTL	strrchr - ANSI C function to search string for rightmost chr.

;+
; Function: strrchr()
;
; Call:
;
;       PUSHL	<character to search for>
;	PUSHL	<offset of search string>
;
;	CALL 	strrchr
;
; Return:
;
;       EAX =  Offset of first occurrence of character, or 0 if not found
;-

p1 =! 8t
p2 =! 12t

	.PSECT	_TEXT_p

	.ENTRY	strrchr

strrchr:
        PUSHL	EDI			;First find end of the string
	MOVSEG	ES, DS			;Copy DS to ES
	CLD				;Scan forward first

; Point to end of string

	MOVL	EDI, p1.B[ESP]		;Get offset of string
	MOVL	ECX, #-1		;Use maximum string size
	CLRL	EAX			;Scan for trailing NUL
	RNSCASB	[EDI]			;Scan to end of string
	NOTL	ECX			;Get length including final null
	DECL	EDI			;Point to final null

; Scan backwards for character, starting at trailing NUL

	MOVB	AL, p2.B[ESP]		;Get character to search for
	STD				;Scan in reverse direction
	RNSCASB	[EDI]			;Search for the character

	LEAL	EAX, 1.B[EDI]		;Return offset of character
	JZ	10$.S			;If really found
	CLRL	EAX			;Not found, so return NULL

10$:	POPL	EDI
	RET

	.END
