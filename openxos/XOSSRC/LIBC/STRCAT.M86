	.TITLE	strcat - ANSI C function to concatenate strings
	.SBTTL	STDC run-time library for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

;+
; Edit history:
;
;  Date    Who  Description
; -------  ---  -----------
;  1Sep94  FPJ  Optimized code.
;-

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

.MACRO	MOVSEG	dst, src, reg
.IF	NB	{reg}
	MOVL	reg, src
	MOVL	dst, reg
.IFF
	PUSHL	src
	POPL	dst
.ENDC
.ENDM

	.SBTTL	strcat - ANSI C function to concatenate strings

;+
; Function: strcat()
;
; Call:
;
;       PUSHL	<offset of source string>
;	PUSHL	<offset of destination string>
;
;	CALL 	strcat
;
; Return:
;
;       EAX =  Offset of destination string
;	EBX =  Random
;	ECX =  Random
;	EDX =  Random
;-

p1 =! 12t
p2 =! 16t

	.PSECT	_TEXT_p

	.ENTRY	strcat

strcat:
	PUSHL	ESI
	PUSHL	EDI
	MOVSEG	ES, DS			;Copy DS to ES
	CLD

; Look for trailing NUL in destination string

	MOVL	EDI, p1.B[ESP]		;Get offset of destination string
	MOVL	EBX, EDI		;And save it
	CLRL	EAX			;Look for trailing NUL
	MOVL	ECX, #-1.B		;Use maximum string size
	RNSCASB	[EDI]			;Scan to end of string
	DECL	EDI			;Point to trailing NUL
	MOVL	EDX, EDI		;Save the destination pointer

	MOVL	EDI, p2.B[ESP]		;Get offset of source string
	MOVL	EBX, EDI		;Save for RMOVSB
;	CLRL	EAX			;Look for trailing NUL
	MOVL	ECX, #-1.B		;Use maximum string size
	RNSCASB	[EDI]			;Scan to end of source string
	NOTL	ECX			;Get length (including final null)

	MOVL	ESI, EBX		;Get offset of source string
	MOVL	EDI, EDX		;Get offset of destination string
	RMOVSB	[EDI], [ESI]		;Copy the string

	MOVL	EAX, p1.B[ESP]		;Return offset of destination string
	POPL	EDI
	POPL	ESI
	RET

	.END
