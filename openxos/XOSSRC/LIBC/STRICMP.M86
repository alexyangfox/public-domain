	.TITLE	stricmp - Function to compare two strings ignoring case
	.SBTTL	Run-time library for XC for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR
	.INCLUD	XOSINC:\XMAC\CTYPE.PAR

	.STDSEGS

	.PSECT	_TEXT_p

	.SBTTL	stricmp - Function to compare two strings ignoring case

;Function to compare two strings
;	const char *stra;	/* Pointer to first string */
;	const char *strb;	/* Pointer to second string */
;	(int)val = stricmp(stra, strb)
;  Value returned is the difference of first not-equal character (0 if
;    strings are the same)

	.ENTRY	stricmp
stricmp:PUSHL	ESI
	MOVL	ESI, 8t.B[ESP]		;Get offset of first string
	MOVL	EBX, 12t.B[ESP]		;Get offset of second string
	CLD
	CLRL	EAX
	CLRL	EDX
2$:	LODSB	[ESI]			;Get character from first string
	TESTB	_chrtbl#[EAX], #CT$LOWER
	JE	4$.S
	SUBB	AL, #20h
4$:	MOVB	DL, [EBX]
	INCL	EBX
	TESTB	_chrtbl#[EDX], #CT$LOWER
	JE	6$.S
	SUBB	DL, #20h
6$:	SUBB	AL, DL
	JNE	10$.S			;If different
	CMPB	DL, #0			;Same - end of string?
	JNE	2$.S			;No - continue
	POPL	ESI			;Yes - finished - return 0
	RET

;Here if strings are different

10$:	MOVXBL	EAX, AL			;Extend difference to 32 bits
	POPL	ESI
	RET

	.END
