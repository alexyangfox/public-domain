	.TITLE	memcpy - ANSI C function to copy memory region
	.SBTTL	STDC run-time library for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

	.PSECT	_TEXT_p

	.SBTTL	memcpy - ANSI C function to copy memory region

;Function to copy a memory area
;	void *memcpy(
;	    void *dst,		// Pointer to destination region
;	    void *src,		// Pointer to source region
;	    int   length);	// Length of region (in bytes)
;  Value returned is dst
;

arg_dst=!12t
arg_src=!16t
arg_len=!20t

	.ENTRY	memcpy
memcpy:
	PUSHL	ESI
	PUSHL	EDI
	PUSHL	DS
	POPL	ES
	CLD
	MOVL	EDI, arg_dst.B[ESP]	;Get offset of destination string
	MOVL	ESI, arg_src.B[ESP]	;Get offset of source string
	MOVL	EBX, arg_len.B[ESP]	;Get amount to copy
	MOVL	EAX, EDI		;Return offset of destination string
        CMPL	EBX, #3.B		;Less than a longword to copy?
        JBE     10$.S                   ;Yes, just copy it and quit
        MOVL    ECX, EDI                ;Get destination address
        NEGL    ECX                     ;Get 2's complement so that
        ANDL    ECX, #3.B               ; we can figure no. of bytes
        SUBL    EBX, ECX                ;  needed for longword alignment
	RMOVSB	[EDI], [ESI]		;Copy 0-3 bytes
        MOVL    ECX, EBX                ;Get the no. of remaining bytes
        SHRL    ECX, #2                 ;Calculate longword count
        RMOVSL  [EDI], [ESI]            ;Move data by longwords
10$:    MOVL    ECX, EBX                ;Calculate the number
        ANDL    ECX, #3.B               ; of bytes left to output
	RMOVSB	[EDI], [ESI]		;Copy 0-3 bytes
	POPL	EDI
	POPL	ESI
	RET

	.END
