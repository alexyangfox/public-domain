	.TITLE	nonport

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

	.PSECT	_TEXT_p

;Function to compare to symbol table entries
;	struct sy *sym1;	/* Address of first symbol table entry */
;	struct sy *sym2;	/* Address of second symbol table entry */
;	val = compsym(sym1, sym2);

compsym::
	PUSHL	ESI
	PUSHL	EDI
	MOVL	ESI, 12t.B[ESP]
	ADDL	ESI, #8t.B
	MOVL	EDI, 16t.B[ESP]
	PUSHL	DS
	POPL	ES
	ADDL	EDI, #8t.B
	MOVL	ECX, #32t
	CLD
	RECMPSB	[EDI], [ESI]
	MOVL	EAX, #1
	JGE	2$.S
	NEGL	EAX
2$:	POPL	EDI
	POPL	ESI
	RET
.PAGE
;Function to search to search opcode, pseudo-op or conditional operator table
;	char   *sym;		/* Address of symbol to search for */
;	struct qqq *tbl;	/* Address of table to search */
;	short  tblsize;		/* Number of entries in table */
;	short  strsize;		/* Length of name entry in table */
;	short  entsize;		/* Total length of entry in table */
;	ent = srchtbl(sym, tbl, tblsize, strsize, entsize);
;Warning:  sym and tbl must be in the static data segment!

srchtbl::
	PUSHL	ESI
	PUSHL	EDI
	MOVL	EBX, 20t.B[ESP]		;Get number of entries in table
	ORL	EBX, EBX		;Zero length table?
	JE	4$.S
	PUSHL	DS
	POPL	ES
	MOVL	EAX, 16t.B[ESP]		;Get offset of table
	MOVL	EDX, 28t.B[ESP]		;Get length of table entry
2$:	MOVL	EDI, EAX		;Setup to compare string
	MOVL	ESI, 12t.B[ESP]
	MOVL	ECX, 24t.B[ESP]
	CLD
	RECMPSB	[EDI], [ESI]		;Compare string
	JE	6$.S			;If match
	ADDL	EAX, EDX		;Not this one - advance to next
	DECL	EBX			;More to check?
	JNE	2$.S			;Yes - continue
4$:	CLRL	EAX			;No - not found - return null
6$:	POPL	EDI
	POPL	ESI
	RET

	.END
