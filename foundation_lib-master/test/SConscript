import os.path

Import("env")

# TEST LIBRARY
testlibsources = [ 'test/test.c' ]

testlibenv = env.Clone()
testlibenv.Append( CPPPATH=['#/test'] )

env['testlib'] = testlibenv.StaticLibrary( 'test', testlibsources )

testlibenv.AddPostAction( 'test', testlibenv.Install( '#lib/${platform}${platformsuffix}/${buildprofile}', [ env['testlib'] ] ) )


# TEST SUITES

testenv    = env.Clone()

testenv.Append( CPPPATH=['#/test'] )
testenv.Append( LIBPATH=['#/lib/${platform}${platformsuffix}/${buildprofile}'] )
testenv.Append( LIBS=[ 'test', 'foundation' ] )

if testenv['platform'] == 'linux':
	testenv.Append( LIBS=[ 'dl', 'rt', 'm' ] )
elif testenv['platform'] == 'raspberrypi':
	testenv.Append( LIBS=[ 'bcm_host', 'rt', 'm' ] )

def makeTest( name ):
    global env
    global testenv
    env[name + 'test'] = testenv.Program( 'test-' + name + '${prgsuffix}', name + '/main.c' )
    testenv.AddPostAction( 'test-' + name + '${prgsuffix}', testenv.Install( '#bin/${platform}${platformsuffix}/${buildprofile}', [ env[name + 'test'] ] ) )

makeTest('all')
makeTest('app')
makeTest('array')
makeTest('atomic')
makeTest('base64')
makeTest('blowfish')
makeTest('bufferstream')
makeTest('config')
makeTest('crash')
makeTest('environment')
makeTest('error')
makeTest('event')
makeTest('fs')
makeTest('hash')
makeTest('hashmap')
makeTest('hashtable')
makeTest('library')
makeTest('math')
makeTest('md5')
makeTest('mutex')
makeTest('objectmap')
makeTest('path')
makeTest('radixsort')
makeTest('ringbuffer')
makeTest('random')
makeTest('semaphore')
makeTest('stacktrace')
makeTest('string')
